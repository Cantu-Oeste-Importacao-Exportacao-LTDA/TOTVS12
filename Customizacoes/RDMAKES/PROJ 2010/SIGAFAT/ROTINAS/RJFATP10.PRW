#INCLUDE "FIVEWIN.CH"
#INCLUDE "TBICONN.CH"
#INCLUDE "TOPCONN.CH"
#INCLUDE "RWMAKE.CH"
                           
/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณRJFATP10  บAutor  ณTOTVS PARANA CENTRALบ Data ณ  05/07/13   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณRotina de manuten็ใo de contratos de redes (Z54/Z55).       บฑฑ
ฑฑบ          ณ                                                            บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณRJU                                                         บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
*---------------------------------------------------------------------------*
User Function RJFATP10()
*---------------------------------------------------------------------------*

PRIVATE cAlias 		:= "Z54"
PRIVATE cTIPOSE1	:= "NCC"
PRIVATE cCadastro	:= OemToAnsi("Contratos de Redes")   

PRIVATE aRotina := { { OemToAnsi("Pesquisar")  		,"AxPesqui"   , 0 , 1} ,;
					 					 { OemToAnsi("Visualizar") 		,"U_RJF10WIN" , 0 , 2} ,;
					 					 { OemToAnsi("Incluir")    		,"U_RJF10WIN" , 0 , 3} ,;
					 					 { OemToAnsi("Alterar")    		,"U_RJF10WIN" , 0 , 4} ,;
					 					 { OemToAnsi("Excluir")    		,"U_RJF10WIN" , 0 , 5} ,;
					 					 { OemToAnsi("Legenda")    		,"U_RJF10LEG" , 0 , 6} }

PRIVATE aCores	   := {	{ "Z54_TIPOCT == 'C' .AND. Z54_ATIVO == 'S'"	, 'BR_VERDE'			},;
												{ "Z54_TIPOCT == 'C' .AND. Z54_ATIVO == 'N'"	, 'BR_VERMELHO'		},;
												{ "Z54_TIPOCT == 'P' .AND. Z54_ATIVO == 'S'"	, 'BR_AMARELO'		},;
												{ "Z54_TIPOCT == 'P' .AND. Z54_ATIVO == 'N'"	, 'BR_PRETO'			} }

PUBLIC STRING_NULL := ""  

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤ
//ณChama fun็ใo para monitor uso de fontes customizadosณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤ
U_USORWMAKE(ProcName(),FunName())   

	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณ Endereca a funcao de BROWSE                                  ณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	
	mBrowse(6, 1, 22, 75, cAlias,,,,,,aCores)
	
Return(.T.)


/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณRJF10WIN  บAutor  ณTOTVS PARANA CENTRALบ Data ณ  05/07/13   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณRotina de manuten็ใo de contratos de redes (Z54/Z55).       บฑฑ
ฑฑบ          ณ                                                            บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณRJU                                                         บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
*---------------------------------------------------------------------------*
User Function RJF10WIN( cAlias, nReg, nOpc, xVar, nOper )       
*---------------------------------------------------------------------------*

Local aArea      := GetArea()
Local aPosObj    := {}
Local aObjects   := {}
Local aSize      := MsAdvSize( .F. )
Local aGet       := {}
Local aTravas    := {}  
Local aRecZ55    := {}
Local aFldsNwTMP := {}                      
Local aFldsNwZ55 := {}
Local aAlterEnch := {}
Local aCpoEnch   := {}
Local nRegE      := 0
Local nModelo    := 3
Local lColumn    := .F. 
Local lF3        := .F. 
Local lMemoria   := .T.
Local lNoFolder  := .F. 
Local lProperty  := .F. 
Local lDelete    :=  IIf ( nOpc == 5 , .T., .F.)
Local bCampo     := {|nCPO| Field(nCPO)}
Local aPages     := {}
Local nOpcNewGd  := IIf ( nOpc == 2 , 0 , GD_INSERT + GD_UPDATE + GD_DELETE )
Local cCHAVE     := Z54->(Z54_NUMERO+Z54_SEQUEN+Z54_TIPOCT+Z54_CLIENT+Z54_LOJACL)
Local cAliasZ54  := "Z54"
Local cSeekZ55   := STRING_NULL
Local cEntidZ55  := "Z55"
Local lGravou    := .F.
Local lTravas    := .T.
Local nCntFor    := 0
Local nUsado     := 0
Local nOpcA      := 0
Local nOpcVisul  := 0
Local nSx8Len    := GetSx8Len()
Local aButtons   := {{ "NOTE", { || U_RJF10ORD() }, OemtoAnsi("Ordenar registros dos itens"), OemtoAnsi("Ordenar") } }

PRIVATE aColsZ55  := {}
PRIVATE aColsZ55E	:= {}
PRIVATE aHeadZ55  := {}
PRIVATE oGetZ55
PRIVATE oDlgEmp

DEFAULT nOper    := 1  

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤ
//ณChama fun็ใo para monitor uso de fontes customizadosณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤ
U_USORWMAKE(ProcName(),FunName())


	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณPega os dados dos campos da tabela SZP no SX3 para montagem do Enchoice         ณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	dbSelectArea(cAliasZ54)
	RegToMemory(cAliasZ54, .T., .T., .T.)                                          
	
	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณAtribui aos Campos registrados os valores do registro da tabela posicionado     ณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	If nOpc != 3
		For i := 1 TO FCount()
			M->&(Eval(bCampo, i)) := FieldGet(i)
		Next i                  
	EndIf
	            
	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณVerifica tํtulo NCC relacionado ao contrato.                                    ณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู

	If nOpc != 3 .and. nOpc != 2
		If Z54->Z54_TIPOCT == "P"
			cPREFIXO := SuperGetMV("MV_X_PRCRP",,"")
			dbSelectArea("SE1")
			dbSetOrder(1)
			dbGoTop()
			If dbSeek(xFilial("SE1")+cPREFIXO+Z54->Z54_NUMERO+Z54->Z54_SEQUEN+cTIPOSE1)
				If SE1->E1_SALDO != SE1->E1_VALOR
					Aviso("Aviso","O tํtulo tipo "+cTIPOSE1+" relacionado a este contrato jแ foi baixado. Nใo ้ possํvel alterar ou excluir este tํtulo.",{"OK"})
					Return
				EndIf
			EndIf
		EndIf
	EndIf
	
	
	dbSelectArea("SX3")
	dbSetOrder(1)
	dbSeek(cAliasZ54)
	While !Eof().and.(x3_arquivo==cAliasZ54)
		If X3USO(x3_usado).and.cNivel>=x3_nivel
			AADD(aCpoEnch,x3_campo)
	  		If nOpc != 5
		   		If ALLTRIM(x3_campo) $ "Z54_NUMERO/Z54_SEQUEN/Z54_TIPOCT/Z54_CLIENT/Z54_LOJACL"
		      		If nOpc != 4
		      			AADD(aAlterEnch,x3_campo)
		      		EndIf
		     	Else
		     		AADD(aAlterEnch,x3_campo)
		     	EndIf
		  	EndIf		   
		Endif
		dbSkip()
	End                     
	
	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณMontagem do aHeadZ55 - TABELA Z55                                        ณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	dbSelectArea("SX3")
	dbSetOrder(1)
	dbSeek(cEntidZ55)
	While ( !SX3->( Eof() ) .And. SX3->X3_ARQUIVO==cEntidZ55 )
		If ( X3Uso(SX3->X3_USADO) .And. cNivel >= SX3->X3_NIVEL .And. ;
				!AllTrim(SX3->X3_CAMPO) $ "Z55_NUMERO/Z55_SEQUEN/Z55_TIPOCT/Z55_CLIENT/Z55_LOJACL" )
			nUsado++
			aAdd(aHeadZ55,{AllTrim(X3Titulo()),;
					              SX3->X3_CAMPO,;
					              SX3->X3_PICTURE,;
					              SX3->X3_TAMANHO,;
					              SX3->X3_DECIMAL,;
					              SX3->X3_VALID,;
					              SX3->X3_USADO,;
					              SX3->X3_TIPO,;
					              SX3->X3_F3,;
					              SX3->X3_CONTEXT,;
					              SX3->X3_CBOX,;
					              SX3->X3_RELACAO,;
					              ".T."})				
			aAdd(aFldsNwTMP, SX3->X3_CAMPO)
		EndIf
		SX3->( dbSkip() )
	EndDo        
	

	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณMontagem do aCols - TABELA Z55                                          ณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู

	If nOpc != 3
	
		dbSelectArea(cEntidZ55)
		dbSetOrder(1)
	 	(cEntidZ55)->(dbGoTop())
	    
		cSeekZ55 := xFilial( cEntidZ55 ) + cCHAVE
	
		If (cEntidZ55)->( MsSeek( cSeekZ55 ) )
			While ( !Eof() .And. cSeekZ55 == Z55->Z55_FILIAL + Z55->(Z55_NUMERO+Z55_SEQUEN+Z55_TIPOCT+Z55_CLIENT+Z55_LOJACL) )
	
				If ( SoftLock(cEntidZ55 ) )
					AAdd(aTravas,{ Alias() , RecNo() })
					AAdd(aColsZ55,Array(nUsado+1))
					AAdd(aRecZ55, (cEntidZ55)->( Recno() ) )
					For nCntFor := 1 To nUsado
						If ( aHeadZ55[nCntFor][10] != "V" )
							aColsZ55[Len(aColsZ55)][nCntFor] := Z55->(FieldGet(FieldPos(aHeadZ55[nCntFor][2])))
						Else
							aColsZ55[Len(aColsZ55)][nCntFor] := CriaVar(aHeadZ55[nCntFor,2])
						EndIf
					Next
					aColsZ55[Len(aColsZ55)][nUsado+1] := .F.
				Else
					lTravas := .F.
				EndIf
				(cEntidZ55)->( dbSkip()   )
			EndDo
		EndIf
	
	EndIf
	
	If Empty( aColsZ55 )
		AAdd(aColsZ55,Array(nUsado+1))
		For nCntFor := 1 To nUsado
			aColsZ55[1,nCntFor] := CriaVar(aHeadZ55[nCntFor,2], .F. )
		Next nCntFor
		aColsZ55[1][nUsado+1] := .F.
	EndIf
	
	If Empty( aColsZ55E )
		AAdd(aColsZ55E,Array(nUsado+1))
		For nCntFor := 1 To nUsado
			aColsZ55E[1,nCntFor] := CriaVar(aHeadZ55[nCntFor,2], .F. )
		Next nCntFor
		aColsZ55E[1][nUsado+1] := .F.
	EndIf
	
	aFldsNwZ55 := aFldsNwTMP

	If ( lTravas )

		AAdd( aObjects, { 080, 100, .T., .F. } )
		AAdd( aObjects, { 100, 100, .T., .T. } )

		aInfo   := { aSize[ 1 ], aSize[ 2 ], aSize[ 3 ], aSize[ 4 ], 3, 2 }
		aPosObj := MsObjSize( aInfo, aObjects )

		DEFINE MSDIALOG oDlgEmp TITLE cCadastro FROM aSize[7],00 TO aSize[6],aSize[5] OF oMainWnd PIXEL

		//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
		//ณ Cabe็alho                                                    ณ
		//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู

		Enchoice(cAliasZ54,nRegE,nOpc,,,,aCpoEnch,{013,aPosObj[1,2],105,aPosObj[1,4]},aAlterEnch,nModelo,,,'',oDlgEmp,lF3,lMemoria,lColumn,,lNoFolder,lProperty)
		
		//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
		//ณ Pastas                                                       ณ
		//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู

		oFolder := TFolder():New( aPosObj[2,1],aPosObj[2,2],{"Verbas/Segmentos"},aPages,oDlgEmp,,,,.T.,.F.,aPosObj[2,4]-aPosObj[2,2],aPosObj[2,3]-aPosObj[2,1])
		
		//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
		//ณ Grid de Exibi็ใo                                             ณ
		//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู			

		oGetZ55 := MsNewGetDados():New( 5, 5, aPosObj[2,3]-aPosObj[2,1]-20,aPosObj[2,4]-aPosObj[2,2]-6, nOpcNewGd, "U_RJF10LCP()", "AllwaysTrue()", , , 000, 99,, STRING_NULL, .T., oFolder:aDialogs[1], aHeadZ55 ,aColsZ55 )

		ACTIVATE MSDIALOG oDlgEmp ON INIT EnchoiceBar(oDlgEmp,{||nOpcA:=1,If(oGetZ55:TudoOk().and.FVLDCONF(),oDlgEmp:End(),nOpcA:=0)},{||oDlgEmp:End()},,aButtons)

		If ( nOpcA == 1 )
			Begin Transaction
				lGravou := RJUFP10GRV( oGetZ55, aCpoEnch, lDelete )
				If ( lGravou )
					EvalTrigger()
					If ( __lSx8 )
						ConfirmSx8()
					EndIf
				EndIf
			End Transaction
			
		EndIf  
		
	EndIf
	
	For nCntFor := 1 To Len(aTravas)
		dbSelectArea(aTravas[nCntFor][1])
		dbGoto(aTravas[nCntFor][2])
		MsUnLock()
	Next nCntFor

	RestArea( aArea )

Return(lGravou) 


/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณRJUFP10GRVบAutor  ณTOTVS PARANA CENTRALบ Data ณ  05/07/13   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณFun็ใo utilizada para gravar os dados.                       ฑฑ
ฑฑบ          ณ                                                            บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณRJU                                                         บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
*---------------------------------------------------------------------------*
Static Function RJUFP10GRV( oGetZ55, aCpoEnch, lDel )
*---------------------------------------------------------------------------*

Local cEntZ54   := "Z54"
Local cEntZ55   := "Z55"
Local lGravou   := .F.
Local lExist    := .F.
Local nLoop     := 0
Local nLoop2    := 0
Private aCols   := oGetZ55:aCols
Private aHeader := oGetZ55:aHeader
Private nPCCLVL := aScan(aHeader,{|x| AllTrim(x[2])=="Z55_CCLVL"})
Private nPCVERBA:= aScan(aHeader,{|x| AllTrim(x[2])=="Z55_CVERBA"})

	Begin Transaction

		If lDel 
			
			//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
			//ณ Exclusใo dos dados                                                     ณ
			//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
					
			dbSelectArea(cEntZ54)
			dbSetOrder(1)
			dbGoTop()
			If dbSeek( xFilial(cEntZ54)+M->(Z54_NUMERO+Z54_SEQUEN+Z54_TIPOCT+Z54_CLIENT+Z54_LOJACL) )

				If RecLock(cEntZ54,.F.)
					(cEntZ54)->(dbDelete())
					(cEntZ54)->(MsUnLock())
				EndIf
        
				//--Exclusใo itens
				dbSelectArea(cEntZ55)
				dbSetOrder(1)
				dbGoTop()
				If dbSeek ( xFilial(cEntZ55) + M->(Z54_NUMERO+Z54_SEQUEN+Z54_TIPOCT+Z54_CLIENT+Z54_LOJACL) )
					While !EOF() .and. Z55->(Z55_FILIAL+Z55_NUMERO+Z55_SEQUEN+Z55_TIPOCT+Z55_CLIENT+Z55_LOJACL) == xFilial(cEntZ55) + M->(Z54_NUMERO+Z54_SEQUEN+Z54_TIPOCT+Z54_CLIENT+Z54_LOJACL)
						If RecLock(cEntZ55,.F.)
							(cEntZ55)->(dbDelete())
							(cEntZ55)->(MsUnLock())						
						EndIf
						dbSkip()
					Enddo				
				EndIf              
				
        		//--Exclusใo tํtulo NCC
				If M->Z54_TIPOCT == "P"				
					FGRVSE1(5)				   
				EndIf
			
			EndIf				
		
		Else
			
			//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
			//ณ Grava dados do cabecalho                                               ณ
			//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู		
			
			dbSelectArea(cEntZ54)
			dbSetOrder(1)
			If dbSeek(xFilial(cEntZ54)+M->(Z54_NUMERO+Z54_SEQUEN+Z54_TIPOCT+Z54_CLIENT+Z54_LOJACL))
				lExist := .T.
			EndIf	
			
			If Len(aCpoEnch) > 0
				If RecLock(cEntZ54,!lExist)
					Z54->Z54_FILIAL := xFilial(cEntZ54)
					For nI := 1 to Len(aCpoEnch)
						&("Z54->"+aCpoEnch[nI]) := &("M->"+aCpoEnch[nI])
					Next nI
					Z54->(MsUnLock())
				EndIf		
			EndIf
		
			//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
			//ณ Grava dados de campos Z55                                              ณ
			//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	
			If Len(aCols) > 0               

				For nLoop := 1 To Len( aCols )
					lGravou := .T.
					If GDDeleted( nLoop )
						If nLoop <= Len( aCols )
							dbSelectArea(cEntZ55)
							dbSetOrder(1)	//--Z55_FILIAL+Z55_NUMERO+Z55_SEQUEN+Z55_TIPOCT+Z55_CLIENT+Z55_LOJACL+Z55_CVERBA+Z55_CCLVL
							dbGoTop()
							If dbSeek ( xFilial(cEntZ55) + M->(Z54_NUMERO+Z54_SEQUEN+Z54_TIPOCT+Z54_CLIENT+Z54_LOJACL) + aCols[nLoop,nPCVERBA] + aCols[nLoop,nPCCLVL] )	
								RecLock( cEntZ55, .F. )
								(cEntZ55)->( dbDelete() )
								(cEntZ55)->( MsUnlock() ) 		
							EndIf
						EndIf
					Else
						If nLoop <= Len( aCols )
			
							dbSelectArea(cEntZ55)
							dbSetOrder(1)	//--Z55_FILIAL+Z55_NUMERO+Z55_SEQUEN+Z55_TIPOCT+Z55_CLIENT+Z55_LOJACL+Z55_CVERBA+Z55_CCLVL
							dbGoTop()
							If dbSeek ( xFilial(cEntZ55) + M->(Z54_NUMERO+Z54_SEQUEN+Z54_TIPOCT+Z54_CLIENT+Z54_LOJACL) + aCols[nLoop,nPCVERBA] + aCols[nLoop,nPCCLVL] )
				        	//--Altera็ใo tํtulo NCC
								If M->Z54_TIPOCT == "P"				
									FGRVSE1(4)				   
								EndIf
								RecLock( cEntZ55, .F. )
							Else			
				        	//--Inclusใo tํtulo NCC
								If M->Z54_TIPOCT == "P"				
									FGRVSE1(3)				   
								EndIf
								RecLock( cEntZ55, .T. ) 		
								Z55->Z55_FILIAL := xFilial( cEntZ55 )
								Z55->Z55_NUMERO := M->Z54_NUMERO
								Z55->Z55_SEQUEN := M->Z54_SEQUEN
								Z55->Z55_TIPOCT := M->Z54_TIPOCT
								Z55->Z55_CLIENT := M->Z54_CLIENT
								Z55->Z55_LOJACL := M->Z54_LOJACL								
							EndIf
				
							//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
							//ณ Grava os demais campos                                                 ณ
							//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
				
							For nLoop2 := 1 To Len( aHeader )
								If ( aHeader[nLoop2,10] <> "V" ) .And. !( AllTrim( aHeader[nLoop2,2] ) $ "Z55_FILIAL/Z55_NUMERO/Z55_SEQUEN/Z55_TIPOCT/Z55_CLIENT/Z55_LOJACL" )
									Z55->(FieldPut(FieldPos(aHeader[nLoop2,2]),aCols[nLoop,nLoop2]))
								EndIf
							Next nLoop2				
							(cEntZ55)->(MsUnlock()) 						
						EndIf
					EndIf
					
				Next nLoop
			EndIf
		EndIf
	
	End Transaction		


Return( lGravou )       


/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณFGRVSE1   บAutor  ณTOTVS PARANA CENTRALบ Data ณ  05/07/13   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณInclusใo/altera็ใo/exclusใo de tํtulo, contas a receber.     ฑฑ
ฑฑบ          ณ                                                            บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณRJU                                                         บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/                          
*---------------------------------------------------------------------------*
Static Function FGRVSE1(nOPERACAO)
*---------------------------------------------------------------------------*
Local aAreaTMP := GetArea()
Local cPREFIXO := SuperGetMV("MV_X_PRCRP",,"")
Local cNATUREZ := SuperGetMV("MV_X_CNCRP",,"")
Local lProcessa:= .T.
Private lMsErroAuto := .F.  

	If nOPERACAO == 4 .or. nOPERACAO == 5
		lProcessa:= .F.
		dbSelectArea("SE1")
		dbSetOrder(2) // FILIAL + CLIENTE + LOJA + PREFIXO + TITULO
		dbGoTop()
		If dbSeek(xFilial("SE1")+M->Z54_CLIENT+M->Z54_LOJACL+cPREFIXO+M->Z54_NUMERO+M->Z54_SEQUEN+cTIPOSE1)	
			lProcessa:= .T.
		EndIf
	EndIf

	If lProcessa

		aDATSE1  := {				{"E1_PREFIXO" ,	cPREFIXO			,Nil},;
									{"E1_NUM"	  , M->Z54_NUMERO		,Nil},;
									{"E1_PARCELA" ,	M->Z54_SEQUEN		,Nil},;
									{"E1_TIPO"	  , cTIPOSE1			,Nil},;
									{"E1_NATUREZ" , cNATUREZ			,Nil},;
									{"E1_CLIENTE" , M->Z54_CLIENT		,Nil},;
									{"E1_LOJA"	  , M->Z54_LOJACL		,Nil},;
									{"E1_EMISSAO" , dDataBase			,Nil},;
									{"E1_VENCTO"  , M->Z54_DATATE		,Nil},;
									{"E1_VENCREA" , M->Z54_DATATE		,Nil},;
									{"E1_HIST" 	  , M->Z54_OBSERV		,Nil},;
									{"E1_VALOR"	  , M->Z54_VALOR		,Nil }}
		
		MSExecAuto({|x,y| Fina040(x,y)},aDATSE1,nOPERACAO) 
			
		If lMsErroAuto
			DisarmTransaction()
			MostraErro()
		Endif
	EndIf
		
	RestArea(aAreaTMP)

Return

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณRJF10LCP  บAutor  ณTOTVS PARANA CENTRALบ Data ณ  05/07/13   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณValida็ใo da MsGetDados - campos.                            ฑฑ
ฑฑบ          ณ                                                            บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณRJU                                                         บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/                          
*---------------------------------------------------------------------------*
User Function RJF10LCP()
*---------------------------------------------------------------------------*
Local lRet 			:= .T.
Local nPCCLVL 	:= aScan(aHeader,{|x| AllTrim(x[2])=="Z55_CCLVL"})
Local nPCVERBA	:= aScan(aHeader,{|x| AllTrim(x[2])=="Z55_CVERBA"})
Local nPPRCDES	:= aScan(aHeader,{|x| AllTrim(x[2])=="Z55_PRCDES"})
Local nPVLRDES	:= aScan(aHeader,{|x| AllTrim(x[2])=="Z55_VLRDES"})

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤ
//ณChama fun็ใo para monitor uso de fontes customizadosณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤ
U_USORWMAKE(ProcName(),FunName())
	
	For nI := 1 to Len(aCols)              
		If ! aCols[n][Len(aCols[n])]         
			//-- verifica se existe registro duplicado
			If n != nI .and. !aCols[nI][Len(aCols[nI])] .and. aCols[nI,nPCCLVL] == aCols[n,nPCCLVL] .and. aCols[nI,nPCVERBA] == aCols[n,nPCVERBA]
				Aviso("Aviso","Este c๓digo de verba e segmento jแ foi informado!",{"OK"})
				lRet := .F.
				Exit
			EndIf
		EndIf
	Next nI	

	If !aCols[n][Len(aCols[n])] .AND. !Empty(aCols[n,nPCCLVL]) .AND. !Empty(aCols[n,nPCVERBA])
		If ( Empty(aCols[n,nPPRCDES]) .AND. Empty(aCols[n,nPVLRDES]) ) .OR. ( !Empty(aCols[n,nPPRCDES]) .AND. !Empty(aCols[n,nPVLRDES]) )
			Aviso("Aviso","Deverแ ser informado o Percentual ou Valor de desconto!",{"OK"})
			lRet := .F.
		EndIf
	EndIf
	
Return (lRet)


/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณFVLDCONF  บAutor  ณTOTVS PARANA CENTRALบ Data ณ  05/07/13   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณValida็ใo sobre confirma็ใo do processo.                     ฑฑ
ฑฑบ          ณ                                                            บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณRJU                                                         บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/                          
*---------------------------------------------------------------------------*
Static Function FVLDCONF()
*---------------------------------------------------------------------------*
Local lRet := .T.
Local nPCCLVL 	:= aScan(oGetZ55:aHeader,{|x| AllTrim(x[2])=="Z55_CCLVL"})
Local nPCVERBA	:= aScan(oGetZ55:aHeader,{|x| AllTrim(x[2])=="Z55_CVERBA"})
	
	If M->Z54_TIPOCT == "P"
	
		If Empty(M->Z54_LOJACL) .OR. Empty(M->Z54_VALOR)
			Aviso("Aviso","Os campos loja cliente e valor devem ser preenchidos para contrato pontual.",{"OK"})
			lRet := .F.
		EndIf
	
	Else

		If Empty(M->Z54_DATADE)
			Aviso("Aviso","O campo data vig๊ncia de deve ser preenchido para este contrato.",{"OK"})
			lRet := .F.
		EndIf
		
		lExist := .F.
		For nI := 1 to Len(oGetZ55:aCols)              
			If ! oGetZ55:aCols[nI][Len(oGetZ55:aCols[nI])]
				If !Empty(oGetZ55:aCols[nI,nPCCLVL]) .AND. !Empty(oGetZ55:aCols[nI,nPCVERBA])         
					lExist := .T.
					Exit
				EndIf
			EndIf
		Next nI	
		If !lExist
			Aviso("Aviso","Deverแ ser informado pelo menos um c๓digo de verba e segmento para este contrato.",{"OK"})
			lRet := .F.
		EndIf
		
	EndIf

Return (lRet)

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณRJF10ORD  บAutor  ณTOTVS PARANA CENTRALบ Data ณ  05/07/13   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณOrdena registros aCols.                                     บฑฑ
ฑฑบ          ณ                                                            บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณRJU                                                         บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
*---------------------------------------------------------------------------*
User Function RJF10ORD()
*---------------------------------------------------------------------------*
Local nPCCLVL 	:= aScan(oGetZ55:aHeader,{|x| AllTrim(x[2])=="Z55_CCLVL"})
Local nPCVERBA	:= aScan(oGetZ55:aHeader,{|x| AllTrim(x[2])=="Z55_CVERBA"})
Private cPerg 	:= "RJFATP10X1"

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤ
//ณChama fun็ใo para monitor uso de fontes customizadosณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤ
U_USORWMAKE(ProcName(),FunName())

	AjustaSX1()
	If Pergunte(cPerg,.T.)
		If mv_par01 == 1
			aSort(oGetZ55:aCols,,,{|x,y| x[nPCVERBA]+x[nPCCLVL] < y[nPCVERBA]+y[nPCCLVL] })
		Else
			aSort(oGetZ55:aCols,,,{|x,y| x[nPCCLVL]+x[nPCVERBA] < y[nPCCLVL]+y[nPCVERBA] })
		EndIf		
		oGetZ55:ForceRefresh()
		oDlgEmp:Refresh()	
	EndIf

Return

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณRJF10ORD  บAutor  ณTOTVS PARANA CENTRALบ Data ณ  05/07/13   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณOrdena registros aCols.                                     บฑฑ
ฑฑบ          ณ                                                            บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณRJU                                                         บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
*---------------------------------------------------------------------------*
Static Function AjustaSX1()
*---------------------------------------------------------------------------*
aRegs  := {} 
	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤ
	//ณDefini็ใo dos itens do grupo de perguntas a ser criadoณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤ
	aAdd(aRegs,{cPerg,"01","Ordenar Registros ?","Ordenar Registros ?","Ordenar Registros ?","mv_ch1","N",01,0,1,"C","","mv_par01","Verba + Segmento","Verba + Segmento","Verba + Segmento","","","Segmento + Verba","Segmento + Verba","Segmento + Verba","","","","","","","","","","","","","","","","", "","","",""})
	
	dbSelectArea("SX1")
	dbSetOrder(1) 
	For i := 1 To Len(aRegs)
		If !dbSeek(cPerg + aRegs[i,2])
			RecLock("SX1", .T.)
			For j := 1 to FCount()
				If j <= Len(aRegs[i])
					FieldPut(j, aRegs[i,j])	 
				Endif
			Next
			MsUnlock()
		Endif
	Next i         
	
Return 

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณRJF10LEG  บAutor  ณTOTVS PARANA CENTRALบ Data ณ  05/07/13   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณRotina para legenda.                                        บฑฑ
ฑฑบ          ณ                                                            บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณRJU                                                         บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
*---------------------------------------------------------------------------*
User Function RJF10LEG()
*---------------------------------------------------------------------------*
Local cTitulo := OemtoAnsi("Legenda")
Local aCores	:= {	{ 'BR_VERDE'		, "Contratual Ativo"			},;
										{ 'BR_VERMELHO' , "Contratual Nใo Ativo"	},;
										{ 'BR_AMARELO'	, "Pontual Ativo"					},;
										{ 'BR_PRETO'		, "Pontual Nใo Ativo"			}}
												
	BrwLegenda(cTitulo, "Legenda", aCores)
	
//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤ
//ณChama fun็ใo para monitor uso de fontes customizadosณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤ
U_USORWMAKE(ProcName(),FunName())

Return   


/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณRJF10TCT  บAutor  ณTOTVS PARANA CENTRALบ Data ณ  05/07/13   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณRotina habilitar/desabilitar MsGetDados.                    บฑฑ
ฑฑบ          ณ                                                            บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณRJU                                                         บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
*---------------------------------------------------------------------------*
User Function RJF10TCT()                                                     
*---------------------------------------------------------------------------*

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤ
//ณChama fun็ใo para monitor uso de fontes customizadosณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤ
U_USORWMAKE(ProcName(),FunName())

	If M->Z54_TIPOCT == "C"
		oGetZ55:ForceRefresh()
		oGetZ55:Enable()		
		oDlgEmp:Refresh()			
	Else
		oGetZ55:aCols := aColsZ55E
		oGetZ55:ForceRefresh()
		oGetZ55:Disable()
		oDlgEmp:Refresh()			
	EndIf
	
Return (M->Z54_TIPOCT)

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณRJF10VCT  บAutor  ณTOTVS PARANA CENTRALบ Data ณ  05/07/13   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณRotina de valida็ใo abilitar MsGetDados.                    บฑฑ
ฑฑบ          ณ                                                            บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณRJU                                                         บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
*---------------------------------------------------------------------------*
User Function RJF10VCT()                                                     
*---------------------------------------------------------------------------* 
Local lRet := .T.
Local aAreaTMP := GetArea()

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤ
//ณChama fun็ใo para monitor uso de fontes customizadosณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤ
U_USORWMAKE(ProcName(),FunName())

	dbSelectArea("Z54")
	dbSetOrder(1)
	dbGoTop()
	If dbSeek( xFilial("Z54")+M->(Z54_NUMERO+Z54_SEQUEN+Z54_TIPOCT+Z54_CLIENT+Z54_LOJACL) )
		MsgAlert("Jแ existe contrato com essas informa็๕es (Tipo, N๚mero, Sequ๊ncia, Cliente e Loja).")
		lRet := .F.
	EndIf
	
	RestArea(aAreaTMP)

Return (lRet)