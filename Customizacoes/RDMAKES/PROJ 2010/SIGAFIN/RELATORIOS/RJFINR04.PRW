#include "rwmake.ch"  
#include "topconn.ch"
#include "protheus.ch"
#include "tbiconn.ch"

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³RJFINR04    ºAutor  ³Rafael Parma      º Data ³  23/02/2011 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Extrato de movimentações do caixinha (SEU).                 º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³RJU                                                         º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/       
*--------------------------*
User Function RJFINR04()         
*--------------------------*

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Declaracao de Variaveis                                             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

Local cDesc1        := "Este programa tem como objetivo imprimir relatorio "
Local cDesc2        := "de acordo com os parametros informados pelo usuario."
Local cDesc3        := ""
Local cPict         := ""
Local titulo        := "EXTRATO CAIXINHA"
Local nLin          := 80
Local Cabec1 		:= "DATA DIG.  HISTÓRICO                  BENEFICIÁRIO                    CÓD.MOT.  NÚM.ADTO         ENTRADA           SAÍDA      SALDO"
Local Cabec2 		:= ""
Local imprime       := .T.
Local aOrd          := {"Data Digitação","Adiantamento","Motorista + Adiantamento"}

Private lEnd        := .F.
Private lAbortPrint := .F.
Private CbTxt       := ""
Private limite      := 132
Private tamanho     := "M"
Private nomeprog    := "RJFINR04" 
Private nTipo       := 18
Private aReturn     := { "Zebrado", 1, "Administracao", 2, 2, 1, "", 1}
Private nLastKey    := 0
Private cbtxt       := Space(10)
Private cbcont      := 00
Private CONTFL      := 01
Private m_pag       := 01
Private wnrel       := "RJFINR04"
Private cPerg       := "RJFINR0004"
Private cString     := "SEU"
Private STRING_NULL := "" 

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ
//³Chama função para monitor uso de fontes customizados³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ
U_USORWMAKE(ProcName(),FunName())

	dbSelectArea(cString)
	dbSetOrder(1)

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Monta a interface padrao com o usuario...                           ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	
	AjustaSX1()
	Pergunte(cPerg, .F.)
	
	wnrel := SetPrint(cString,NomeProg,cPerg,@titulo,cDesc1,cDesc2,cDesc3,.T.,aOrd,.T.,Tamanho,,.T.)
	
	If nLastKey == 27
		Return
	Endif
	
	SetDefault(aReturn,cString)
	
	If nLastKey == 27
	   Return
	Endif
	
	nTipo := If(aReturn[4]==1,15,18)
	
	nOrdem:= aReturn[8]
	 
	If nOrdem == 1
		titulo := ALLTRIM(titulo) + " - POR DATA DIGITAÇÃO"
	ElseIf nOrdem == 2        
		titulo := ALLTRIM(titulo) + " - POR ADIANTAMENTO"
	Else
		titulo := ALLTRIM(titulo) + " - POR MOTORISTA + ADIANTAMENTO"
	EndIf
	
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Processamento. RPTSTATUS monta janela com a regua de processamento. ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	
	RptStatus({|| RunReport(Cabec1,Cabec2,Titulo,nLin,nOrdem) },Titulo)
	
	
Return

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºFun‡„o    ³RUNREPORT º Autor ³ Rafael Parma       º Data ³  22/09/10   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDescri‡„o ³ Funcao auxiliar chamada pela RPTSTATUS. A funcao RPTSTATUS º±±
±±º          ³ monta a janela com a regua de processamento.               º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ Programa principal                                         º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

*---------------------------------------------------------------------*
Static Function RunReport(Cabec1,Cabec2,Titulo,nLin,nOrdem)
*---------------------------------------------------------------------*

Local cQuery    := STRING_NULL
Local cAliasTMP := GetNextAlias()
Local nTOTENT := 0
Local nTOTSAI := 0
Local nTOTSLD := 0

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Filtro de registros                                                 ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ

	cQuery := "	SELECT																	" + chr(13)+ chr(10)
	cQuery += "			SEU.EU_FILIAL,													" + chr(13)+ chr(10)
	cQuery += "			SEU.EU_TIPO,													" + chr(13)+ chr(10)
	cQuery += "			SEU.EU_CAIXA,													" + chr(13)+ chr(10)
	cQuery += "			SEU.EU_HISTOR,													" + chr(13)+ chr(10)
	cQuery += "			SEU.EU_BENEF,													" + chr(13)+ chr(10)
	cQuery += "			SEU.EU_CODMOT,													" + chr(13)+ chr(10)
	cQuery += "			SEU.EU_NROADIA,													" + chr(13)+ chr(10)
	cQuery += "			SEU.EU_DTDIGIT,													" + chr(13)+ chr(10)	
	cQuery += "			SEU.EU_VALOR													" + chr(13)+ chr(10)
	cQuery += "	FROM " + RetSQLName("SEU") + " SEU		   								" + chr(13)+ chr(10)
	cQuery += "	WHERE 									   								" + chr(13)+ chr(10)
	cQuery += "			SEU.EU_FILIAL = '" + xFilial("SEU") + "' 						" + chr(13)+ chr(10)
	cQuery += "	AND		SEU.EU_DTDIGIT BETWEEN											" + chr(13)+ chr(10)
	cQuery += "			'" + dtos(mv_par01) + "' AND '" + dtos(mv_par02) + "'			" + chr(13)+ chr(10)
	cQuery += "	AND		SEU.EU_CODMOT BETWEEN											" + chr(13)+ chr(10)
	cQuery += "			'" + mv_par03 + "' AND '" + mv_par04 + "'						" + chr(13)+ chr(10)
	cQuery += "	AND		SEU.EU_CAIXA = '" + mv_par05 + "'								" + chr(13)+ chr(10)
	cQuery += "	AND		SEU.D_E_L_E_T_ != '*'											" + chr(13)+ chr(10)
	cQuery += "	ORDER BY																" + chr(13)+ chr(10)
	cQuery += "			SEU.EU_FILIAL,													" + chr(13)+ chr(10)
	If nOrdem == 1
		cQuery += "		SEU.EU_DTDIGIT 													" + chr(13)+ chr(10)
	ElseIf nOrdem == 2                	
		cQuery += "		SEU.EU_NROADIA,													" + chr(13)+ chr(10)
		cQuery += "		SEU.EU_DTDIGIT 													" + chr(13)+ chr(10)
	Else
		cQuery += "		SEU.EU_CODMOT,													" + chr(13)+ chr(10)
		cQuery += "		SEU.EU_NROADIA,													" + chr(13)+ chr(10)
		cQuery += "		SEU.EU_DTDIGIT 													" + chr(13)+ chr(10)
	EndIf
	
	
	memowrite("RJFINR04.SQL",cQuery)
	
	TCQUERY ChangeQuery( cQuery ) NEW ALIAS (cAliasTMP)
	
	dbSelectArea(cAliasTMP)
	(cAliasTMP)->(dbGoTop())
	If !(cAliasTMP)->(EOF())
		nTOTSLD := fCalcSLD()
	EndIf
	SetRegua((cAliasTMP)->(RecCount()))
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Percorre itens selecionados para impressão                          ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	
	While (cAliasTMP)->(!EOF())

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Verifica o cancelamento pelo usuario...                             ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	
		If lAbortPrint
			@nLin,00 PSAY "*** CANCELADO PELO OPERADOR ***"
			Exit
		Endif
	
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Impressao do cabecalho do relatorio. . .                            ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	
		If nLin > 55 
			Cabec(Titulo,Cabec1,Cabec2,NomeProg,Tamanho,nTipo)
			//-- Imprime saldo anterior do caixinha na primeira página
			If nLin == 80
				nLin := 8
				@nLin,000 PSAY "SALDO ANTERIOR: "
				@nLin,121 PSAY nTOTSLD	PICTURE "@E 9,999,999.99"				
				nLin += 2
			Else
				nLin := 8
			EndIf

		Endif

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Impressão dos dados                                                 ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ
	    
		//cabec1 := "DATA DIG.  HISTÓRICO                  BENEFICIÁRIO                    CÓD.MOT.  NÚM.ADTO         ENTRADA           SAÍDA        SALDO"
		//           0123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012
		//           0         1         2         3         4         5         6         7         8         9         10        11        12        13
		If (cAliasTMP)->EU_TIPO == "00" .AND. !Empty((cAliasTMP)->EU_NROADIA)
			(cAliasTMP)->(dbSkip())
			Loop	
		EndIf
		

		@nLin,000 PSAY DTOC(STOD((cAliasTMP)->EU_DTDIGIT))
		@nLin,011 PSAY (cAliasTMP)->EU_HISTOR		
		@nLin,038 PSAY SUBSTR((cAliasTMP)->EU_BENEF,1,30)
		@nLin,070 PSAY (cAliasTMP)->EU_CODMOT		
		@nLin,080 PSAY (cAliasTMP)->EU_NROADIA
		If (cAliasTMP)->EU_TIPO $ "02/10"
			@nLin,092 PSAY (cAliasTMP)->EU_VALOR	PICTURE "@E 9,999,999.99"
			nTOTSLD += (cAliasTMP)->EU_VALOR
			nTOTENT += (cAliasTMP)->EU_VALOR
		Else
			@nLin,109 PSAY (cAliasTMP)->EU_VALOR	PICTURE "@E 9,999,999.99"		
			nTOTSLD -= (cAliasTMP)->EU_VALOR
			nTOTSAI += (cAliasTMP)->EU_VALOR
		EndIf
		@nLin,121 PSAY nTOTSLD 	PICTURE "@E 9,999,999.99"

		nLin := nLin + 1 
	    
		IncRegua()
		(cAliasTMP)->(dbSkip())
	   
	EndDo
	
	(cAliasTMP)->(dbCloseArea())


	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Impressão dos totalizadores                                         ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ

	If nTOTSAI != 0 .or. nTOTENT != 0
        
		nLin := nLin + 1 
		@nLin,000 PSAY "TOTAL: "
		@nLin,092 PSAY nTOTENT	PICTURE "@E 9,999,999.99"
		@nLin,109 PSAY nTOTSAI	PICTURE "@E 9,999,999.99"
		@nLin,121 PSAY nTOTSLD	PICTURE "@E 9,999,999.99"
	
	EndIf
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Finaliza a execucao do relatorio...                                 ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	
	SET DEVICE TO SCREEN
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Se impressao em disco, chama o gerenciador de impressao...          ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	
	If aReturn[5]==1
	   dbCommitAll()
	   SET PRINTER TO
	   OurSpool(wnrel)
	Endif
	
	MS_FLUSH()

Return



/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³fCalcSLD   ºAutor  ³Rafael Parma       º Data ³  23/02/2011 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Função utilizada para cálculo do saldo anterior do caixinha.º±±
±±º          ³            .                                               º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³RJU                                                         º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

*--------------------------*
Static Function fCalcSLD()  
*--------------------------*
Local nSLDANT := 0
Local cDATANT := ""
Local cAliasSLD := GetNextAlias()


	
	/*
	If nOrdem != 3	// Ordem diferente de Motorista, no caso de ordem por motorista recupera saldo de todos os movimentos.

		//-- Recupera último registro de transferência de saldo do caixinha
	
		cQuery := "	SELECT		MAX(SEU.R_E_C_N_O_) SEURECNO				   					" + chr(13)+ chr(10)
		cQuery += "	FROM " + RetSQLName("SEU") + " SEU						   					" + chr(13)+ chr(10)
		cQuery += "	WHERE		SEU.EU_FILIAL   = '" + xFilial("SEU") + "'						" + chr(13)+ chr(10)
		cQuery += "	AND			SEU.EU_CAIXA    = '" + mv_par05 + "'							" + chr(13)+ chr(10)
		cQuery += "	AND			SEU.EU_TIPO     = '11'											" + chr(13)+ chr(10) 
		cQuery += "	AND			SEU.D_E_L_E_T_ != '*'											" + chr(13)+ chr(10)
	
		TCQUERY ChangeQuery( cQuery ) NEW ALIAS (cAliasSLD)
		
		dbSelectArea(cAliasSLD)
		(cAliasSLD)->(dbGoTop())
		If !(cAliasSLD)->(EOF())
			If (cAliasSLD)->SEURECNO > 0
				nLASTRE := (cAliasSLD)->SEURECNO
			EndIf
		EndIf
		(cAliasSLD)->(dbCloseArea())
	
	EndIf
		
	
	//-- Recalcula o saldo até a data de inicio do relatório.
	cQuery := "	SELECT 	SUM(CASE WHEN ( SEU.EU_TIPO = '02' OR SEU.EU_TIPO = '10' ) THEN 	" + chr(13)+ chr(10)
	cQuery += "				SEU.EU_VALOR ELSE (	CASE WHEN ( SEU.EU_TIPO >= '90' OR 			" + chr(13)+ chr(10)
	cQuery += "		 		(SEU.EU_TIPO = '00'  AND SEU.EU_NROADIA != '          ' ) ) 	" + chr(13)+ chr(10)
	cQuery += "		 		THEN 0 ELSE (SEU.EU_VALOR*(-1)) END ) END ) SALDO				" + chr(13)+ chr(10)
	cQuery += "	FROM " + RetSQLName("SEU") + " SEU											" + chr(13)+ chr(10)
	cQuery += "	WHERE		SEU.EU_FILIAL   = '" + xFilial("SEU") + "'						" + chr(13)+ chr(10)
	cQuery += "	AND			SEU.EU_DTDIGIT  < '" + dtos(mv_par01) + "'						" + chr(13)+ chr(10)
	cQuery += "	AND			SEU.EU_CODMOT BETWEEN											" + chr(13)+ chr(10)
	cQuery += "				'" + mv_par03 + "' AND '" + mv_par04 + "'						" + chr(13)+ chr(10)		
	cQuery += "	AND			SEU.EU_CAIXA    = '" + mv_par05 + "'							" + chr(13)+ chr(10)
	cQuery += "	AND			SEU.R_E_C_N_O_  > " + cValToChar(nLASTRE) + "					" + chr(13)+ chr(10) 
	cQuery += "	AND			SEU.D_E_L_E_T_ != '*'											" + chr(13)+ chr(10)
	
	*/

	cQuery := "	SELECT		MAX(FIJ.FIJ_DATA) FIJ_DATA					   					" + chr(13)+ chr(10)
	cQuery += "	FROM " + RetSQLName("FIJ") + " FIJ						   					" + chr(13)+ chr(10)
	cQuery += "	WHERE		FIJ.FIJ_FILIAL   = '" + xFilial("FIJ") + "'						" + chr(13)+ chr(10)
	cQuery += "	AND			FIJ.FIJ_CODCX    = '" + mv_par05 + "'							" + chr(13)+ chr(10)
	cQuery += "	AND			FIJ.FIJ_DATA     < '" + dtos(mv_par01) + "'						" + chr(13)+ chr(10)
	cQuery += "	AND			FIJ.D_E_L_E_T_ != '*'											" + chr(13)+ chr(10)	

	TCQUERY ChangeQuery( cQuery ) NEW ALIAS (cAliasSLD)
	
	dbSelectArea(cAliasSLD)
	(cAliasSLD)->(dbGoTop())
	If !(cAliasSLD)->(EOF())	
		cDATANT := (cAliasSLD)->FIJ_DATA
	EndIf
	(cAliasSLD)->(dbCloseArea())	

    
	If cDATANT != ""
		
		cQuery := "	SELECT		FIJ_SALDO									   					" + chr(13)+ chr(10)
		cQuery += "	FROM " + RetSQLName("FIJ") + " FIJ						   					" + chr(13)+ chr(10)
		cQuery += "	WHERE		FIJ.FIJ_FILIAL   = '" + xFilial("FIJ") + "'						" + chr(13)+ chr(10)
		cQuery += "	AND			FIJ.FIJ_CODCX    = '" + mv_par05 + "'							" + chr(13)+ chr(10)
		cQuery += "	AND			FIJ.FIJ_DATA     = '" + cDATANT  + "'  							" + chr(13)+ chr(10)
		cQuery += "	AND			FIJ.D_E_L_E_T_ != '*'											" + chr(13)+ chr(10)	
	
		TCQUERY ChangeQuery( cQuery ) NEW ALIAS (cAliasSLD)
		
		dbSelectArea(cAliasSLD)
		(cAliasSLD)->(dbGoTop())
		If !(cAliasSLD)->(EOF())	
			nSLDANT := (cAliasSLD)->FIJ_SALDO
		EndIf
		(cAliasSLD)->(dbCloseArea())	
	
	EndIf

Return (nSLDANT)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³AJUSTASX1  ºAutor  ³Rafael Parma       º Data ³  23/02/2011 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Função utilizada para verificar/criar no ambiente o grupo   º±±
±±º          ³de perguntas.                                               º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³RJU                                                         º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

*--------------------------*
Static Function AjustaSX1()
*--------------------------*
aRegs  := {} 

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ
	//³Definição dos itens do grupo de perguntas a ser criado³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ
 	aAdd(aRegs,{cPerg,"01","Data Digitação De   ?","Data Digitação De   ?","Data Digitação De   ?","mv_ch1","D",08,0,0,"G","","mv_par01","","","","","","","","","","","","","","","","","","","","","","","","", "","","",""})
	aAdd(aRegs,{cPerg,"02","Data Digitação Até  ?","Data Digitação Até  ?","Data Digitação Até  ?","mv_ch2","D",08,0,0,"G","","mv_par02","","","","","","","","","","","","","","","","","","","","","","","","", "","","",""})
	aAdd(aRegs,{cPerg,"03","Motorista De        ?","Motorista De        ?","Motorista De        ?","mv_ch3","C",TAMSX3("EU_CODMOT")[1],0,0,"G","","mv_par03","","","","","","","","","","","","","","","","","","","","","","","","", "DA4","","",""})
	aAdd(aRegs,{cPerg,"04","Motorista Até       ?","Motorista Até       ?","Motorista Até       ?","mv_ch4","C",TAMSX3("EU_CODMOT")[1],0,0,"G","","mv_par04","","","","","","","","","","","","","","","","","","","","","","","","", "DA4","","",""})
	aAdd(aRegs,{cPerg,"05","Caixinha            ?","Caixinha            ?","Caixinha            ?","mv_ch5","C",TAMSX3("EU_CAIXA")[1] ,0,0,"G","","mv_par05","","","","","","","","","","","","","","","","","","","","","","","","", "SET","","",""})
	
	dbSelectArea("SX1")
	dbSetOrder(1) 
	
	For i := 1 To Len(aRegs)
		If !dbSeek(cPerg + aRegs[i,2])
			RecLock("SX1", .T.)
			For j := 1 to FCount()
				If j <= Len(aRegs[i])
					FieldPut(j, aRegs[i,j])	 
				Endif
			Next
			MsUnlock()
		Endif
	Next         

	
Return Nil