#INCLUDE "TOTVS.CH"                                                             
#INCLUDE "FILEIO.CH"
#INCLUDE "RWMAKE.CH"
#INCLUDE "TOPCONN.CH"
#INCLUDE "TBICONN.CH"


/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณRJUJOBL1    บAutor  ณRafael Parma      บ Data ณ  27/06/12   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณJob para processamento de registros da tabela SL1, realiza aบฑฑ
ฑฑบ          ณatualiza็ใo do campo L1_SITUA para registros com erro 'ER'. บฑฑ
ฑฑบ          ณExecutado atrav้s do schedule (U_RJUJOBL1(Empresa, Filial)) บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณCANTU                                                       บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
*---------------------------------------------------------------------------*
User Function RJUJOBL1(cCODEMP, cCODFIL)               
*---------------------------------------------------------------------------*
Private nHdlLog := nil
Private cArqLog := "RJUJOBL1_"+dtos(DATE())+"_"+StrTran(cValToChar(time()),":","")+".log"  // arquivo de log das opera็๕es  

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤ
//ณChama fun็ใo para monitor uso de fontes customizadosณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤ
U_USORWMAKE(ProcName(),FunName())

	
ConOut(Time() + " - " + cCODEMP + cCODFIL + " - RJUJOBL1 - " + "INICIO")

	If cCODEMP != nil .and. cCODFIL != nil

		RpcClearEnv()              
		RPCSetType(3)            
	    PREPARE ENVIRONMENT EMPRESA cCODEMP FILIAL cCODFIL MODULO "FAT" TABLES "SL1" 
	    
	    cEnvSrv := GetEnvServer()
	    cRpoDb  := ALLTRIM(Upper(GetPvProfString(cEnvSrv, "RpoDb", "DBF", GetADV97())))
	    
	    dbSelectArea("SL1")
		dbSetOrder(1)
		dbGoTop()    
		dbSeek(cCODFIL)          
		While !SL1->(EOF()) .AND. SL1->L1_FILIAL == cCODFIL
			If ALLTRIM(SL1->L1_SITUA) == "ER" .AND. SL1->L1_X_PJOB != "S"    			
				If RecLock("SL1",.F.)
					//--VERIFICA A VERSAO DO RPO QUE O JOB ESTA SENDO EXECUTADO, CASO SEJA DBF E' NA ESTACAO, CASO CONTRARIO NA RETAGUARDA.
					If cRpoDB == "DBF"	
						//--PDV
						SL1->L1_SITUA	:= "00"
					Else	   
						//--RETAGUARDA
						SL1->L1_SITUA	:= "RX"
					EndIf
					SL1->L1_X_PJOB	:= "S"					
					SL1->(MsUnLock())
				EndIf				
			ElseIf ALLTRIM(SL1->L1_SITUA) == "ER" .AND. SL1->L1_X_PJOB == "S"
				If nHdlLog == nil
					If !fBegFile()
						CONOUT(TIME()+"RJUJOBL1-PROBLEMA NA CRIACAO DO ARQUIVO DE LOG.")
					EndIf
				EndIf
				If nHdlLog != nil
					cTEXTO := "L1_FILIAL = "+SL1->L1_FILIAL+" | L1_NUM = "+SL1->L1_NUM+" | L1_CLIENTE = "+SL1->L1_CLIENTE+" | L1_LOJA = "+SL1->L1_LOJA+" | L1_DOC = "+SL1->L1_DOC+" | L1_SERIE = "+SL1->L1_SERIE 
					fWrtFile(cTEXTO)
				EndIf
			EndIf			
			SL1->(dbSkip())
		Enddo

	Else
		CONOUT(TIME()+"RJUJOBL1-PROBLEMA COM O CODIGO DA EMPRESA/FILIAL.")
	EndIf
	
	If nHdlLog != nil
		fEndFile()
	EndIf	
	
ConOut(Time() + " - " + cCODEMP + cCODFIL + " - RJUJOBL1 - " + "FIM")
		
Return



/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณfBegFile    บAutor  ณRafael Parma      บ Data ณ  11/12/09   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณInicializa็ใo do arquivo de log.                            บฑฑ
ฑฑบ          ณ                                                            บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณTOTVS                                                       บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/

Static Function fBegFile()
Local lRet := .T.

	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณ Cria o arquivo texto                                                ณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู

	cDestino := "\RJULOBL1\"

	If !File(cDestino)
		MAKEDIR(cDestino)
	EndIf
		
	nHdlLog := fCreate((cDestino+cArqLog))

	If nHdlLog == -1
	    lRet := .F.
	Endif
	
Return (lRet)

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณfWrtFile    บAutor  ณRafael Parma      บ Data ณ  11/12/09   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณEscreve no arquivo de log.                                  บฑฑ
ฑฑบ          ณ                                                            บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณTOTVS                                                       บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/

Static Function fWrtFile(cLin)
Local lRet := .T.
Local cSTR := cLin
	
	cSTR := dtos(ddatabase)+"-"+cValToChar(time())+"-"+cSTR+chr(13)+chr(10)
	
   	If fWrite(nHdlLog,cSTR,Len(cSTR)) != Len(cSTR)
   		lRet := .F.
   	EndIf
	
Return (lRet)

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณfEndFile    บAutor  ณRafael Parma      บ Data ณ  11/12/09   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณFinaliza็ใo do arquivo de log.                              บฑฑ
ฑฑบ          ณ                                                            บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณTOTVS                                                       บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/

Static Function fEndFile()

	fClose(nHdlLog)
	
Return
