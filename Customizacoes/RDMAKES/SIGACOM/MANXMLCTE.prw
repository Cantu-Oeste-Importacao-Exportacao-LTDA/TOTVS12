#INCLUDE "PROTHEUS.CH"                                          
#INCLUDE "TOPCONN.CH"
#INCLUDE "RWMAKE.CH"
#INCLUDE "FILEIO.CH"
#INCLUDE "TBICONN.CH"
#INCLUDE "TBICODE.CH"

#DEFINE NQTDDIA 365                 // Apenas serão mostrados na tela os xmls cuja data_emissao >= (data_atual - NQTDDIA)
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³MANXMLCTE     ºAutor ³TOTVS II           Data ³  21/10/14   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Rotina de manutenção dos arquivos .xml referente ao Ct-e   º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ Específicos Cantu                                          º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
*-------------------------*
User Function MANXMLCTE()  
*-------------------------*                                    

Static oBtnCan
Static oBtnFec
Static oBtnGer
Static oBtnImp
Static oBtnLog
Static oGrpBot
Static oGrpMan
Static oLbCon
Static oLbLeg   
Static oCboSta
Static oBtnVer 

Private oLbSta                             // Label da régua de processamento
Private oMtProc                            // Objeto da régua de processamento
Private nMtProc  := 0                      // Número máximo de movimentos da régua de processamento
Private oDados                             // Objeto do grid
Private aCpos    := {}                     // Vetor que vai armazenar a propriedade dos campos do grid
Private cTrab    := ""                     // Variável que receberá a estrutura de campos da tab. temporária
Private cIndic   := ""                     // Índice da tab. temporária
Private aCampos  := {}                     // Armazena as Pictures dos campos
Private aCores   := {}                     // Armazena o vetor de cores
Private lInverte := .F.                    // Indica se a marcação do grid deve ser invertida 
Private cMarca   := GetMark()
Private oDlgMan
Private oMark
Private lFndXML  := .F. 
Private nCboSta  := '1' 

SetKey(VK_F7 ,{||U_MANPARCTE()})
SetKey(VK_F6 ,{||fLegenda()})
SetKey(VK_F9 ,{||fGrvChv()})
SetKey(VK_F10,{||U_SCHCLACTE(.T.)})   

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ
//³Chama função para monitor uso de fontes customizados³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ

U_USORWMAKE(ProcName(),FunName())

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Avalia se o componente do grid já existe.³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

if Select("TMP_GRID") > 0
	TMP_GRID->(DbCloseArea())
EndIf

aCpos := {}

aAdd(aCpos, {"MARK"    , "C" , 04, 0})
aAdd(aCpos, {"FILIAL"  , "C" , TamSX3("F1_FILIAL" )[01], 0})
aAdd(aCpos, {"ARQUIVO" , "C" , TamSX3("ZBC_FILE"  )[01], 0})
aAdd(aCpos, {"SERIE"   , "C" , TamSX3("F1_SERIE"  )[01], 0})
aAdd(aCpos, {"DOCUMEN" , "C" , TamSX3("F1_DOC"    )[01], 0})
aAdd(aCpos, {"EMISSAO" , "D" , TamSX3("F1_EMISSAO")[01], 0})
aAdd(aCpos, {"FORNECE" , "C" , TamSX3("F1_FORNECE")[01], 0})
aAdd(aCpos, {"LOJA"    , "C" , TamSX3("F1_LOJA"   )[01], 0})
aAdd(aCpos, {"VLTOTAL" , "N" , TamSX3("F1_VALBRUT")[01], TamSX3("F1_VALBRUT")[02]})
aAdd(aCpos, {"VLFRETE" , "N" , TamSX3("F1_FRETE"  )[01], TamSX3("F1_FRETE"  )[02]})
aAdd(aCpos, {"STATUS"  , "C" , TamSX3("ZBC_PROCES")[01], 0})
aAdd(aCpos, {"PESO"    , "N" , TamSX3("ZBC_PESO"  )[01], TamSX3("ZBC_PESO"  )[02]})
aAdd(aCpos, {"VOLUME"  , "N" , TamSX3("ZBC_VOLUME")[01], TamSX3("ZBC_VOLUME")[02]})
aAdd(aCpos, {"CALC"    , "N" , TamSX3("ZBC_CALC  ")[01], TamSX3("ZBC_CALC"  )[02]})
                                                              
cTrab  := CriaTrab(aCpos)
cIndic := CriaTrab(NIL, .F.)

dbUseArea( .T.,,cTrab,"TMP_GRID",.F. )
dbSelectArea("TMP_GRID")
cChave1  := "ARQUIVO"

IndRegua("TMP_GRID",cIndic,cChave1,,,"Selecionando Registros...")
dbSelectArea("TMP_GRID")

aAdd(aCampos, {"FILIAL"  , "", "Filial"     , X3Picture("F1_FILIAL" )})
aAdd(aCampos, {"ARQUIVO" , "", "Nome XML"   , X3Picture("F1_FILE"   )})
aAdd(aCampos, {"SERIE"   , "", "Serie NF"   , X3Picture("F1_SERIE"  )})
aAdd(aCampos, {"DOCUMEN" , "", "Nro. Docum" , X3Picture("F1_DOC"    )})
aAdd(aCampos, {"EMISSAO" , "", "Dt. Emiss"  , X3Picture("F1_EMISSAO")})
aAdd(aCampos, {"FORNECE" , "", "Fornecedor" , X3Picture("F1_FORNECE")})
aAdd(aCampos, {"LOJA"    , "", "Loja"       , X3Picture("F1_LOJA"   )})
aAdd(aCampos, {"VLTOTAL" , "", "Vlr NFe Ori", X3Picture("F1_VALBRUT")})
aAdd(aCampos, {"VLFRETE" , "", "Vlr Frete " , X3Picture("F1_FRETE"  )})
aAdd(aCampos, {"STATUS"  , "", "Status"     , X3Picture("ZBC_PROCES")})
aAdd(aCampos, {"PESO"    , "", "Peso Bruto" , X3Picture("ZBC_PESO"  )})
aAdd(aCampos, {"VOLUME"  , "", "Volume"     , X3Picture("ZBC_VOLUME")})
aAdd(aCampos, {"CALC"    , "", "Frt/Vlr NFe", X3Picture("ZBC_CALC"  )})

aAdd(aCores,{"TMP_GRID->STATUS == '1'" , "BR_BRANCO"   })
aAdd(aCores,{"TMP_GRID->STATUS == '2'" , "BR_LARANJA"    })
aAdd(aCores,{"TMP_GRID->STATUS == '3'" , "BR_VERMELHO" })
aAdd(aCores,{"TMP_GRID->STATUS == '4'" , "BR_AMARELO"  })
aAdd(aCores,{"TMP_GRID->STATUS == '5'" , "BR_CINZA"    })
aAdd(aCores,{"TMP_GRID->STATUS == '6'" , "BR_AZUL"     })
aAdd(aCores,{"TMP_GRID->STATUS == '7'" , "BR_VERDE"})

  DEFINE MSDIALOG oDlgMan TITLE "Manipulação do Ct-e" FROM 000, 000  TO 500, 1000 COLORS 0, 16777215 PIXEL

    @ 002, 002 GROUP oGrpMan TO 248, 499                                      OF oDlgMan COLOR 0, 16777215 PIXEL
    @ 004, 004 GROUP oGrpBot TO 040, 497 PROMPT "   Botões de Manipulação   " OF oGrpMan COLOR 0, 16777215 PIXEL
    
    @ 235, 313 SAY oLbLeg PROMPT "F6 - Legenda"       SIZE 036, 007 OF oGrpMan COLORS 0, 16777215 PIXEL
    @ 235, 358 SAY oLbCon PROMPT "F7 - Configurações" SIZE 055, 007 OF oGrpMan COLORS 0, 16777215 PIXEL
    @ 011, 334 SAY oLbSta PROMPT " "                  SIZE 123, 007 OF oGrpMan COLORS 0, 16777215 PIXEL    
    
    oMark := MsSelect():New("TMP_GRID", , , aCampos, @lInverte, cMarca, {042, 004, 229, 497},,,,,aCores)
 		ObjectMethod(oMark:oBrowse,"Refresh()")
 		oMark:oBrowse:bLDblClick := {|| fShowKey(Trim(TMP_GRID->ARQUIVO))}
		oMark:oBrowse:Refresh()
    
    @ 232, 450 BUTTON oBtnFec PROMPT "&Fechar"           SIZE 043, 012 ACTION oDlgMan:End() OF oGrpMan PIXEL
    @ 017, 010 BUTTON oBtnImp PROMPT "Importar XMLs"     SIZE 046, 012 ACTION fImport()     OF oGrpMan PIXEL
    @ 017, 058 BUTTON oBtnGer PROMPT "Gerar Pré-Nota"    SIZE 045, 012 WHEN fFndXML()                  ACTION fGerPre()     OF oGrpMan PIXEL
    @ 017, 105 BUTTON oBtnCan PROMPT "Cancelar Pré-Nota" SIZE 056, 012 WHEN (TMP_GRID->STATUS == '2')  ACTION fCanPre(TMP_GRID->FILIAL, TMP_GRID->DOCUMEN, TMP_GRID->SERIE,; 
    																																																									TMP_GRID->FORNECE, TMP_GRID->LOJA, Trim(TMP_GRID->ARQUIVO))    OF oGrpMan PIXEL
    @ 017, 164 BUTTON oBtnLog PROMPT "Detalhes "         SIZE 045, 012 WHEN (TMP_GRID->STATUS $ '3/4') ACTION fLogPro(Trim(TMP_GRID->ARQUIVO)) OF oGrpMan PIXEL
    @ 017, 211 BUTTON oBtnVer PROMPT "Ver Depois "       SIZE 045, 012 WHEN (TMP_GRID->STATUS $ '1/3/5/6') ACTION fVerDep(Trim(TMP_GRID->ARQUIVO)) OF oGrpMan PIXEL
    @ 017, 258 BUTTON oBtnPrc PROMPT "Proc Individ "     SIZE 045, 012 WHEN (TMP_GRID->STATUS $ '1/4') ACTION fPrcMan(Trim(TMP_GRID->ARQUIVO)) OF oGrpMan PIXEL
    
    @ 021, 334 METER oMtProc VAR nMtProc SIZE 151, 008 OF oGrpMan TOTAL 100 NOPERCENTAGE COLOR 0, 16777215 PIXEL 
    
    @ 232, 017 MSCOMBOBOX oCboSta VAR nCboSta ITEMS {"1=Nao Iniciado","2=Pré-nota gerada","3=Erro Proc",; 
    																								 "4=Ver Depois", "5=Pré-Nota Cancel","6=Evento Processado",; 
    																								 "7=Pré-nota classificada", "8=Todos"} SIZE 071, 010 OF oGrpMan ON CHANGE fDados() COLORS 0, 16777215 PIXEL

    fDados()
    
  ACTIVATE MSDIALOG oDlgMan CENTERED

Return                                                             

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³fShowKey ºAutor  ³TOTVS II           º Data ³  07/05/15     º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Função criada para retornar Chave do Ct-e                  º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ MANXMLCTE                                                  º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

*----------------------------*
Static Function fShowKey(cFile)
*----------------------------*

Local cChvCte := ""
Local oXML         

Private cAviso := ""
Private cErro  := ""

DbSelectArea("ZBC")
ZBC->(DbSetOrder(2))
If ZBC->(DbSeek(xFilial("ZBC") + cFile))
	
	oXML := XMLParser(AnsiToOem(ZBC->ZBC_XML), "_", @cAviso, @cErro)
		
	if ValType(oXML) == "O"
		if Alltrim(Upper(XMLGetChild(oXML, 1):REALNAME)) != "PROCEVENTOCTE"
			cChvCte := oXML:_cteproc:_protcte:_infprot:_chcte:text
		Else
			MsgAlert("XML REFERENTE A UM EVENTO. SOMENTE PODERAO SER CONSULTADOS CHAVES DE XMLS DE CTE.")
		EndIf
		
	Else
		
		MsgAlert("ARQUIVO "+ Trim(aXMLs[nX][01]) +" => O XML POSSUI CARACTERES ESPECIAIS. NAO SERA POSSIVEL PROCESSA-LO!")
			
	EndIf		
EndIf

if !Empty(cChvCte)
	Aviso("Chave do Ct-e", cChvCte, {"Ok"}, 3)
EndIf

Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³fPrcMan ºAutor  ³TOTVS II           º Data ³  25/02/15      º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Função para processar individualmente cada XML             º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ MANXMLCTE                                                  º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
*------------------------------*
Static Function fPrcMan(cFile)
*------------------------------*

Local aArea  := GetArea()
Local nCtrl  := 0
Local aXMLs  := {}
Local lOk    := .F.
Local cAviso := ""
Local cErro  := ""
Local oXML   

Private aCabec := {}          	// vetor com cabeçalho da pré-nota
Private aItens := {}          	// vetor de itens da pré-nota
Private aLinha := {}        	// vetor da linha do item 
Private lOk    := {}				// indica se o processo de inclusão de pré-nota foi completado com sucesso
Private aErros := {}          	// variável que armazena as inconsistências encontradas durante os processos
Private aXMLs  := {}          	// vetor com os xmls a serem processados
Private aResErr  := {}        	// vetor com o resumo das inconsistências
Private aResAce  := {}        	// Vetor com o resumo das operações que concluíram

If MsgYesNo("Deseja processar manualmente arquivo "+ cFile +" ?", "Atenção!")
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Valida existência de parâmetros da rotina antes de iniciar o processamento.³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	
	DbSelectArea("ZBA")
	ZBA->(DbSetOrder(1))
	If !ZBA->(DbSeek(xFilial("ZBA") + cEmpAnt + cFilAnt))
		MsgInfo("A aplicação encontrou um problema: não foi possível encontrar os parâmetros da rotina!" + CHR(13)+CHR(10) +;
						"Solução: Antes de iniciar, cadastre os parâmetros necessários através do botão F7.")
		Return          
	EndIf 

	DbSelectArea("ZBC")
	ZBC->(DbSetOrder(2))
	If ZBC->(DbSeek(xFilial("ZBC") + cFile))
		
		aAdd(aXMLs, {ZBC->ZBC_FILE, ZBC->ZBC_XML, ZBC->(Recno())})	
		oMtProc:SetTotal(Len(aXMLs))     // Seta o tamanho da régua
		
		nCtrl++
		oMtProc:Set(nCtrl)
		oLbSta:SetText("Processando XML final ..."+ SubStr(Trim(aXMLs[01][01]), Len(Trim(aXMLs[01][01])) - 15, 16))
		oDlgMan:CommitControls()
		
		//fClear(aXMLs[01][02])
		aXMLs[01][02] := fClear(aXMLs[01][02])
		
		oXML := XMLParser(AnsiToOem(aXMLs[01][02]), "_", @cAviso, @cErro)
		
		if ValType(oXML) == "O"
    	
    	lEveCan := .F.
			lCancel := .F.
		
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Valida se o XML é referente a um evento de cancelamento³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			
			if Alltrim(Upper(XMLGetChild(oXML, 1):REALNAME)) == "PROCEVENTOCTE"
				lEveCan := oXML:_procEventoCte:_eventoCte:_infEvento:_tpEvento:TEXT == '110111'    // 110111 - Evento de cancelamento (Manual Ct-e 2.0)
			EndIf
			
			oLbSta:SetText(iif(lEveCan, "Cancelando", "Gerando") +" pré-nota XML final ..."+ SubStr(Trim(aXMLs[01][01]), Len(Trim(aXMLs[01][01])) - 15, 16))
			oDlgMan:CommitControls()		
		
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Chama a função de importação do arquivo XML³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			
			If lEveCan
				
				cChvCte := oXML:_procEventoCte:_eventoCte:_infEvento:_chCte:text
	
				if !Empty(cChvCte)
				  DBSelectArea("ZBC")
				  ZBC->(DbSetOrder(4))
				  If DbSeek(xFilial("ZBC") + cChvCte)
				  	If ZBC->ZBC_PROCES == '2'
				  		lCancel := fCanPre(ZBC->ZBC_CODFIL, ZBC->ZBC_DOC, ZBC->ZBC_SERIE, ZBC->ZBC_FORNECE, ZBC->ZBC_LOJA, Trim(ZBC->ZBC_FILE))	
				  	Else
				  		lCancel := .T.
				  	EndIf
				  	ZBC->(Recno())
				  Else
				  	Aviso("Ct-e não encontrado", "O Ct-e de origem contendo a chave "+ Trim(cChvCte) +" não foi encontrado! ", {"Ok"}, 3)
				  EndIf
				EndIf
				
			Else
				lOk := fExecXML(oXML, Trim(aXMLs[01][01]))
			EndIf
			
			if lOk
				DbSelectArea("ZBC")
				ZBC->(DbGoTo(aXMLs[01][03]))
	
				RecLock("ZBC", .F.)
				ZBC->ZBC_PROCES := '2'	
				ZBC->(MsUnlock())
			
			ElseIf lCancel
				
				DbSelectArea("ZBC")
				ZBC->(DbGoTo(aXMLs[nX][03]))
	
				RecLock("ZBC", .F.)
				ZBC->ZBC_PROCES := '6'	
				ZBC->(MsUnlock())
				
				ZBC->(DbGoTo(nRecCte))
				RecLock("ZBC", .F.)
				ZBC->ZBC_PROCES := '5'	
				ZBC->(MsUnlock())
					
			EndIf
			
  	Else
  		MsgAlert("ARQUIVO "+ Trim(aXMLs[01][01]) +" => O XML POSSUI CARACTERES ESPECIAIS. NAO SERA POSSIVEL PROCESSA-LO!")
		EndIf
			
	EndIf		
EndIf

fDados()

oMtProc:Set(0)
oLbSta:SetText(" ")
oDlgMan:CommitControls()

cResAce := ""                                                   
cResErr := ""

If Len(aResErr) > 0
	For i := 1 to Len(aResErr)
		cResErr += aResErr[i] + CHR(13)+CHR(10) + Replicate("-", 100) + CHR(13)+CHR(10)
	Next i	
	Aviso("Resumo de Inconsistências", cResErr, {"Ok"}, 3)
	cResErr := ""
	aResErr := {}
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Mensagem para resumo dos acertos³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

If Len(aResAce) > 0
	For j := 1 to Len(aResAce)
		cResAce += aResAce[j] + CHR(13)+CHR(10)
	Next j                                  
	Aviso("Resumo de XMLs processados com sucesso", cResAce, {"Ok"}, 3)
	cResAce := ""
	aResAce := {}
EndIf

RestArea(aArea)

Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³fVerDep ºAutor  ³TOTVS II           º Data ³  25/02/15      º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Função criada para mudar status do registro para '4'       º±±
±±º          ³ (Ver Depois)                                               º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ MANXMLCTE                                                  º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Static Function fVerDep(cFile)

Local aArea := GetArea()

DbSelectArea("ZBC")
ZBC->(DbSetOrder(2))
If ZBC->(DbSeek(xFilial("ZBC") + cFile)) 
	RecLock("ZBC", .F.)
	ZBC->ZBC_PROCES := '4'
	ZBC->(MsUnlock())
EndIf

fDados()

oMtProc:Set(0)
oLbSta:SetText(" ")
oDlgMan:CommitControls()

RestArea(aArea)

Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³fFndXML ºAutor  ³TOTVS II              º Data ³  06/01/15   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Função criada para retornar se existem XMLs a serem        º±±
±±º          ³ processados na tabela ZBC                                  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ MANXMLCTE                                                  º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Static Function fFndXML()

Local lRet := .F.

DbSelectArea("ZBC")                    	
ZBC->(DbSetOrder(3))
lRet := ZBC->(DbSeek(xFilial("ZBC") + cEmpAnt + cFilAnt + '1'))

Return lRet

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Função responsável por fazer a exclusão da pré-nota³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
*------------------------*
Static Function fCanPre(cFilXML, cDoc, cSerie, cFornece, cLoja, cFile)
*------------------------*

Local cChave  := cFilXML + cDoc + cSerie + cFornece + cLoja
Local nRecSF1 := 0
Local aRecSD1 := {}
Local lExclui := .T.
Local lRet    := .F.
Local nX      := 0

DbSelectArea("SF1")
SF1->(DbSetOrder(1))
If SF1->(DbSeek(cChave))
	nRecSF1 := SF1->(Recno())
	
	If !MsgYesNo("Deseja realmente excluir a pré-nota?","Atenção!")
		Return
	EndIf
	
	DbSelectArea("SD1")
	SD1->(DbSetOrder(1))
	if SD1->(DbSeek( cChave )) 
		While !SD1->(EOF()) .and. SD1->(D1_FILIAL+D1_DOC+D1_SERIE+D1_FORNECE+D1_LOJA) == cChave .and. lExclui
      If Empty(SD1->D1_TES)
      	aAdd(aRecSD1, SD1->(Recno()))
      Else
      	Alert("Impossível excluir a pré-nota! O documento já se encontra classificado.")
      	lExclui :=.F.
      EndIf
			SD1->(DbSkip())
		EndDo
	EndIf
	
	If lExclui
	
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Exclui os itens da pré-nota.³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	
		For nX := 1 to Len(aRecSD1) 
			SD1->(DbGoTo(aRecSD1[nX]))
			RecLock("SD1", .F.)
			SD1->(DbDelete())
			SD1->(MsUnlock())
		Next nX           
	
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄH
		//³Deleta cabeçalho da pré-nota de entrada³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		
		DbSelectArea("SF1")
		SF1->(DbGoTo(nRecSF1))
		RecLock("SF1", .F.)
		SF1->(DbDelete())
		SF1->(MsUnlock())
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Volta status do XML na tabela ZBC³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		
		DbSelectArea("ZBC")
		ZBC->(DbSetOrder(2))
		If ZBC->(DbSeek(xFilial("ZBC") +cFile))
			RecLock("ZBC", .F.)
			ZBC->ZBC_PROCES := '1'
			ZBC->(MsUnlock())
		EndIf
		
		lRet := .T.
		                                      
		Aviso("Documento excluído!","Exclusão de pré-nota efetuada com sucesso!" +CHR(13)+CHR(10)+;
																PADR("Filial"    , 20, ".")+": "+ cFilXML    +CHR(13)+CHR(10)+;
																PADR("Documento" , 20, ".")+": "+ cDoc       +CHR(13)+CHR(10)+;
																PADR("Serie"     , 20, ".")+": "+ cSerie     +CHR(13)+CHR(10)+;
																PADR("Fornecedor", 20, ".")+": "+ cFornece   +CHR(13)+CHR(10)+;
																PADR("Loja"      , 20, ".")+": "+ cLoja      , {"Ok"}, 3)
		
	EndIf
	
Else
	Alert("Não foi possível localizar pré-nota com os dados abaixo: "  +CHR(13)+CHR(10)+;
												PADR("Filial"    , 20, ".")+": "+ cFilXML    +CHR(13)+CHR(10)+;
												PADR("Documento" , 20, ".")+": "+ cDoc       +CHR(13)+CHR(10)+;
												PADR("Serie"     , 20, ".")+": "+ cSerie     +CHR(13)+CHR(10)+;
												PADR("Fornecedor", 20, ".")+": "+ cFornece   +CHR(13)+CHR(10)+;
												PADR("Loja"      , 20, ".")+": "+ cLoja) 
EndIf

fDados()

oMtProc:Set(0)
oLbSta:SetText(" ")
oDlgMan:CommitControls()
	
Return lRet  

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Função responsável por mostrar o log de processamento do XML³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
*----------------------*
Static Function fLogPro(cFile)
*----------------------*

Default cFile := "" 

Static oMemo            
Static oBtnClose
Static oDlgXML

Private cErro := ""

If Empty(cFile)

	MsgAlert("Clique em uma linha para depois visualizar os logs!")
	Return

Else
	
	DbSelectArea("ZBC")
	DbSetOrder(2)
	DbSeek(xFilial("ZBC") + cFile)
	cErro := AnsiToOem(ZBC->ZBC_LOG)                   
	
EndIf
  
	DEFINE MSDIALOG oDlgXML TITLE "Erros de integração do Ct-e " FROM 000,000 TO 500,900 COLORS 0,16777215 PIXEL
	@ 015,015 GET oMemo VAR cErro MEMO SIZE 420,200 OF oDlgXML PIXEL READONLY
	@ 220,380 BUTTON oBtnClose PROMPT "&Fechar"  SIZE 056, 013 OF oDlgXML ACTION Close(oDlgXML) PIXEL
	
	ACTIVATE MSDIALOG oDlgXML CENTERED
	
Return

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ
//³Função responsável por mostrar o Browse das legendas de cores
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ 

*--------------------------*
Static Function fLegenda()
*--------------------------*

Local cTitulo := OemtoAnsi("Legenda")
Local aCores	:= {	{ 'BR_BRANCO' 	, "Processo não iniciado"  		},;
										{ 'BR_LARANJA'  , "Pré-nota gerada"	          },;
										{ 'BR_VERMELHO'	, "Erro de processamento"  		},;
										{ 'BR_AMARELO'  , "Verificar depois"          },;
										{ 'BR_CINZA'    , "Ct-e Cancelado"            },;
										{ 'BR_AZUL'     , "Evento Executado"          },;
										{ 'BR_VERDE'    , "Pré-nota classificada"     }}											
												
BrwLegenda(cTitulo, "Legenda", aCores)

Return

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Função responsável por abrir o XML e realizar a geração das pré-notas³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
*------------------------*
Static Function fGerPre()
*------------------------*

Local oXml 
local cErro  := ""
local cAviso := ""                                            
local lOK    := .F.
Local cTemp  := ""
Local cQuery := ""
Local nQuant := 0
Local nCtrl  := 0                                                     
Local nX     := 0
Local i      := 0

Private aCabec := {}          // vetor com cabeçalho da pré-nota
Private aItens := {}          // vetor de itens da pré-nota
Private aLinha := {}          // vetor da linha do item 
Private lOk    := {}					// indica se o processo de inclusão de pré-nota foi completado com sucesso
Private aErros := {}          // variável que armazena as inconsistências encontradas durante os processos
Private aXMLs  := {}          // vetor com os xmls a serem processados
Private aResErr  := {}        // vetor com o resumo das inconsistências
Private aResAce  := {}        // Vetor com o resumo das operações que concluíram

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Valida existência de parâmetros da rotina antes de iniciar o processamento.³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

DbSelectArea("ZBA")
ZBA->(DbSetOrder(1))
If !ZBA->(DbSeek(xFilial("ZBA") + cEmpAnt + cFilAnt))
	MsgInfo("A aplicação encontrou um problema: não foi possível encontrar os parâmetros da rotina!" + CHR(13)+CHR(10) +;
					"Solução: Antes de iniciar, cadastre os parâmetros necessários através do botão F7.")
	Return          
EndIf

DbSelectArea("ZBC")
ZBC->(DBGoTop())
ZBC->(DbSetOrder(3))
If ZBC->(DbSeek(xFilial("ZBC") + cEmpAnt + cFilAnt + '1'))
	
	While !ZBC->(EOF()) .and. (ZBC->ZBC_FILIAL + ZBC->ZBC_CODEMP + ZBC->ZBC_CODFIL + ZBC->ZBC_PROCES == xFilial("ZBC") + cEmpAnt + cFilAnt + '1' )
	
		aAdd(aXMLs, {ZBC->ZBC_FILE, ZBC->ZBC_XML, ZBC->(Recno())})
 		ZBC->(DbSkip())

	EndDo
	
	oMtProc:SetTotal(Len(aXMLs))     // Seta o tamanho da régua
	
	For nX := 1 to Len(aXMLs)
		
		nCtrl++
		oMtProc:Set(nCtrl)
		oLbSta:SetText("Processando XML final ..."+ SubStr(Trim(aXMLs[nX][01]), Len(Trim(aXMLs[nX][01])) - 15, 16))
		oDlgMan:CommitControls()
		
		aXMLs[nX][02] := fClear(aXMLs[nX][02])
		
		oXML := XMLParser(AnsiToOem(aXMLs[nX][02]), "_", @cAviso, @cErro)
		
		if ValType(oXML) == "O"		
		  
			lEveCan := .F.
			lCancel := .F.
			nRecCte := 0
		
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Valida se o XML é referente a um evento de cancelamento³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			
			if Alltrim(Upper(XMLGetChild(oXML, 1):REALNAME)) == "PROCEVENTOCTE"
				lEveCan := oXML:_procEventoCte:_eventoCte:_infEvento:_tpEvento:TEXT == '110111'    // 110111 - Evento de cancelamento (Manual Ct-e 2.0)
			EndIf
			
    	oLbSta:SetText(iif(lEveCan, "Cancelando", "Gerando") +" pré-nota XML final ..."+ SubStr(Trim(aXMLs[nX][01]), Len(Trim(aXMLs[nX][01])) - 15, 16))
			oDlgMan:CommitControls()
		
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Chama a função de importação do arquivo XML³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			
			If lEveCan
				
				cChvCte := oXML:_procEventoCte:_eventoCte:_infEvento:_chCte:text
	
				if !Empty(cChvCte)
				  DBSelectArea("ZBC")
				  ZBC->(DbSetOrder(4))
				  If DbSeek(xFilial("ZBC") + cChvCte)
				  	If ZBC->ZBC_PROCES == '2'
				  		lCancel := fCanPre(ZBC->ZBC_CODFIL, ZBC->ZBC_DOC, ZBC->ZBC_SERIE, ZBC->ZBC_FORNECE, ZBC->ZBC_LOJA, Trim(ZBC->ZBC_FILE))	
				  	Else
				  		lCancel := .T.
				  	EndIf
				  	nRecCte := ZBC->(Recno())
				  Else
				  	Aviso("Ct-e não encontrado", "O Ct-e de origem contendo a chave "+ Trim(cChvCte) +" não foi encontrado! ", {"Ok"}, 3)
				  EndIf
				EndIf
				
			Else
				lOk := fExecXML(oXML, Trim(aXMLs[nX][01]))
			EndIf
			
			if lOk
			
				DbSelectArea("ZBC")
				ZBC->(DbGoTo(aXMLs[nX][03]))
	
				RecLock("ZBC", .F.)
				ZBC->ZBC_PROCES := '2'	
				ZBC->(MsUnlock())
			
			ElseIf lCancel
				
				DbSelectArea("ZBC")
				ZBC->(DbGoTo(aXMLs[nX][03]))
	
				RecLock("ZBC", .F.)
				ZBC->ZBC_PROCES := '6'	
				ZBC->(MsUnlock()) 
				
				ZBC->(DbGoTo(nRecCte))
				RecLock("ZBC", .F.)
				ZBC->ZBC_PROCES := '5'	
				ZBC->(MsUnlock())
					
			EndIf
			
  	Else
  		MsgAlert("ARQUIVO "+ Trim(aXMLs[nX][01]) +" => O XML POSSUI CARACTERES ESPECIAIS. NAO SERA POSSIVEL PROCESSA-LO!")
		EndIf
			
	Next nX 

Else
	MsgInfo("Não há XMLs a serem processados!")			
EndIf

fDados()

oMtProc:Set(0)
oLbSta:SetText(" ")
oDlgMan:CommitControls()

cResAce := ""                                                   
cResErr := ""

If Len(aResErr) > 0
	For i := 1 to Len(aResErr)
		cResErr += aResErr[i] + CHR(13)+CHR(10)
	Next i	
	Aviso("Resumo de Inconsistências", cResErr, {"Ok"}, 3)
	cResErr := ""
	aResErr := {}
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Mensagem para resumo dos acertos³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

If Len(aResAce) > 0
	For j := 1 to Len(aResAce)
		cResAce += aResAce[j] + CHR(13)+CHR(10)
	Next j                                  
	Aviso("Resumo de XMLs processados com sucesso", cResAce, {"Ok"}, 3)
	cResAce := ""
	aResAce := {}
EndIf

Return

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Função que executa o processo de importação dos XMLs³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

*------------------------*
Static Function fImport()
*------------------------*

local aFiles  := {}       // arquivos encontrados no diretório
Local aSize   := {}       // variável com os tamanhos de arquivos
Local cDiret  := ""       // diretório onde estão os XMLs
Local nTaman  := 0        // tamanho do arquivo em bytes
Local cBuffer := ""       // conteúdo do XML
Local nImport := 0        // número de arquivos importados
Local nAtual  := 0        // número de arquivos atualizados  

Private oXML
Private cAviso := ""
Private cErro  := ""
Private lAuto  := .F.

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Valida existência de parâmetros da rotina antes de iniciar o processamento.³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

DbSelectArea("ZBA")
ZBA->(DbSetOrder(1))
If ZBA->(DbSeek(xFilial("ZBA") + cEmpAnt + cFilAnt))
	lAuto := ZBA->ZBA_GERPRE == '1'
Else
	MsgInfo("A aplicação encontrou um problema: não foi possível encontrar os parâmetros da rotina!" + CHR(13)+CHR(10) +;
					"Solução: Antes de iniciar, cadastre os parâmetros necessários através do botão F7.")
	Return          
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄeriĞt¿
//³Valida a existência do diretório especificado no parâmetro da rotina ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄeriĞtÙ

if !ExistDir(Trim(ZBA->ZBA_DIRORI))
	MsgInfo("A pasta "+ Trim(ZBA->ZBA_DIRORI) +" configurada nos parâmetros da rotina não existe. Verifique a configuração através do botão F7.")
	Return
Else
	cDiret := Trim(ZBA->ZBA_DIRORI)
EndIf

aFiles := {}

ADir(cDiret + "*.xml", aFiles, aSize )

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄA¿
//³Avalia se existe arquivos no diretório³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄAÙ

If Len(aFiles) > 0
	
	oMtProc:SetTotal(Len(aFiles))  

	For nX := 1 to Len(aFiles)
	  oMtProc:Set(nX)
		oLbSta:SetText("Processando arquivo "+ aFiles[nX])
		oDlgMan:CommitControls()
	  
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Testa a abertura do arquivo XML antes de iniciar a importação³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		
	  nHdl := fOpen(cDiret + Trim(aFiles[nX]), 0)
	  if nHdl == -1
	  	MsgAlert("Não foi possível fazer a leitura do arquivo "+ aFiles[nX] +" :( " )
	  	fClose(nHdl)
	  	Loop
	  Else 
	  
	    cBuffer := ""
	  	nTaman  := fSeek(nHdl,0,FS_END)
	  	fseek(nHdl,0,FS_SET)
	  	fRead(nHdl,@cBuffer,nTaman)
	  	
	  	oXML := XMLParser(cBuffer, "_", @cAviso, @cErro)
      
			aRetEmp := {}
	  
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ8¿
			//³Valida se o XML é de um Ct-e ou de um evento³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ8Ù
			
	  	if AllTrim(Upper(XMLGetChild(oXML, 1):REALNAME)) == "CTEPROC" 
	    	lEvento := .F.
	    	aRetEmp := fGetRem(lEvento)
		  	If Len(aRetEmp) > 0
			  	cEmpDes := aRetEmp[01]
			  	cFilDes := aRetEmp[02]
			  Else
			  	MsgAlert( Trim(aFiles[nX])+ " => ERRO! REMETENTE DO FRETE NAO E NENHUMA FILIAL DA CANTU.")
		  		Loop	
			  EndIf
	  	
	  	ElseIf Alltrim(Upper(XMLGetChild(oXML, 1):REALNAME)) == "PROCEVENTOCTE"
      	
      	lEvento := .T.
    
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ0¿
				//³Valida se o evento é de cancelamento³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ0Ù
				
      	If oXML:_procEventoCte:_eventoCte:_infEvento:_tpEvento:TEXT == '110111'    // 110111 - Evento de cancelamento (Manual Ct-e 2.0)
      	
	      	aRetEmp := fGetRem(lEvento)
	      	If Len(aRetEmp) > 0
				  	cEmpDes := aRetEmp[01]
				  	cFilDes := aRetEmp[02]
				  Else
			  		Loop	
				  EndIf
				  
				EndIf
			EndIf
	  	
	  	DbSelectArea("ZBC")
	  	DbSetOrder(2)
	  	If DbSeek(xFilial("ZBC") + Trim(aFiles[nX]))
	  		if MsgYesNo("O arquivo "+ Trim(aFiles[nX]) +" já foi importado. Deseja importá-lo novamente?", "Registro encontrado!")
		  		RecLock("ZBC", .F.)
		  		ZBC->ZBC_FILIAL := xFilial("ZBC")
		  		ZBC->ZBC_CODEMP := cEmpDes
		  		ZBC->ZBC_CODFIL := cFilDes
		  		ZBC->ZBC_FILE   := Trim(aFiles[nX])
		  		ZBC->ZBC_LOCAL  := cDiret
		  		ZBC->ZBC_XML    := OemToAnsi(Iif(SubStr(cBuffer, 01, 03) == 'ï»¿', SubStr(cBuffer, 04), cBuffer))
		  		ZBC->ZBC_PROCES := '1'
		  		ZBC->(MsUnlock())
		  		nAtual++
	  		EndIf
	  	Else
	  		RecLock("ZBC", .T.)
		  		ZBC->ZBC_FILIAL := xFilial("ZBC")
		  		ZBC->ZBC_CODEMP := cEmpDes
		  		ZBC->ZBC_CODFIL := cFilDes
		  		ZBC->ZBC_FILE   := Trim(aFiles[nX])
		  		ZBC->ZBC_LOCAL  := cDiret
		  		ZBC->ZBC_XML    := OemToAnsi(Iif(SubStr(cBuffer, 01, 03) == 'ï»¿', SubStr(cBuffer, 04), cBuffer))
		  		ZBC->ZBC_PROCES := '1'
	  		ZBC->(MsUnlock())  
	      nImport++
	  	EndIf
	  	
	  	fClose(nHdl)                      // Fecha XML após importação
	  	fErase(cDiret + Trim(aFiles[nX])) // Deleta XML após importação
	  	
	  EndIf
	  
	Next Nx
	
	Aviso("Resumo", AllTrim(cValToChar(nAtual))  +" arquivos atualizados."+ CHR(13)+CHR(10) +;
									AllTrim(cValToChar(nImport)) +" arquivos incluídos. ", {"Ok"}, 2)
	
Else
	MsgInfo("Nenhum arquivo encontrado no diretório "+ cDiret +" com extensão .xml " )
	Return
EndIf                                                                                   

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄo
//³Caso o parâmetro esteja habilitado, processa automaticamente os xmls.³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄo

If lAuto
	If MsgYesNo("Deseja fazer o processamento dos XMLs pendentes? ", "Atenção!")
		fGerPre()
	EndIf
EndIf

fDados()

oMtProc:Set(0)
oLbSta:SetText(" ")
oDlgMan:CommitControls()
                                                                                                                  
Return 

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³fGetDes ºAutor  ³TOTVS II              º Data ³  22/01/15   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Função para buscar Empresa e Filial Emitente.              º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                        º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Static Function fGetRem(lEvento)

Local aRet    := {}
Local aArea   := SM0->(GetArea())
Local cCgcRem := ""
Local cSql    := ""
Local cChvCte := ""

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄL¿
//³Avalia se o xml é um evento para saber de onde deve pegar o xml.³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄLÙ

if lEvento
	
	cChvCte := oXML:_procEventoCte:_eventoCte:_infEvento:_chCte:text
	
	if !Empty(cChvCte)
	  DBSelectArea("ZBC")
	  ZBC->(DbSetOrder(4))
	  
	  If DbSeek(xFilial("ZBC") + cChvCte)
	  	aRet := {ZBC->ZBC_CODEMP, ZBC->ZBC_CODFIL}
	  	
	  	RestArea(aArea)
	  	Return aRet
	  Else
	  	Aviso("Origem não encontrada","O CT-e de chave "+ cChvCte +" não foi importado através dessa rotina! "+; 
	  	      "A exclusão da pré-nota deverá ser feita manualmente.", {"Ok"}, 3)
	  	RestArea(aArea)
	  	Return aRet	
	  EndIf
	  
	EndIf
Else
	cCgcRem := oXML:_cteproc:_cte:_infcte:_rem:_cnpj:text
EndIf

DbSelectArea("SM0")
SM0->(DbSetOrder(1))
SM0->(DbGoTop())
While !SM0->(EOF())
	If Trim(SM0->M0_CGC) == AllTrim(cCgcRem)
		aRet := {SM0->M0_CODIGO, SM0->M0_CODFIL}
		Exit
	EndIf
	SM0->(DbSkip())	
EndDo

RestArea(aArea)

Return aRet

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ
//³Função de montagem dos dados do grid³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ

*------------------------*
Static Function fDados(nExec)
*------------------------*

Local cSql    := ""
Local aArea   := GetArea()

Default nExec := 2

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Elimina todo o conteúdo do grid antes de buscar as informações atualizadas³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

DbSelectArea("TMP_GRID")
TMP_GRID->(DbGoTop())
Do While !TMP_GRID->(Eof())
	RecLock("TMP_GRID",.F.)
	DbDelete()
	MsUnlock() 
	TMP_GRID->(DbSkip())
Enddo

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Buscando os xmls cujos processos ainda não foram finalizados³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

cSql := "select zbc.zbc_codfil, zbc.zbc_file, zbc.zbc_proces, zbc.zbc_serie, zbc.zbc_doc, zbc.zbc_emissa, "
cSql += "       zbc.zbc_fornec, zbc.zbc_loja, zbc.zbc_vltot, zbc.zbc_vlfre, zbc.zbc_peso, zbc.zbc_volume, "
cSql += "       zbc.zbc_calc from "+ RetSqlName("ZBC") +" zbc "
cSql += "where zbc.zbc_filial = '"+ xFilial("ZBC") +"' "
cSql += "  and zbc.zbc_codemp = '"+ cEmpAnt +"' "
cSql += "  and zbc.zbc_codfil = '"+ cFilAnt +"' "
cSql += "  and (zbc.zbc_emissa = '        ' or zbc.zbc_emissa >= '"+ DtoS(Date() - NQTDDIA) +"') "

If nCboSta != '8'
	cSql += "   and zbc.zbc_proces = '"+ nCboSta +"' "
EndIf

cSql += "  and zbc.d_e_l_e_t_ = ' ' "

TcQuery cSql New Alias "tmpctes"

DbSelectArea("tmpctes")
tmpctes->(DbGoTop())

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Avalia se o retorno da busca for vazia³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

If tmpctes->(eof())

	If nExec == 2
		TMP_GRID->(DbGoTop())
		ObjectMethod(oMark:oBrowse,"Refresh()")
		oDlgMan:Refresh()
	EndIf
  RestArea(aArea)
	tmpctes->(DbCloseArea())
	Return
EndIf

While !tmpctes->(EOF())
	
	DbSelectArea("TMP_GRID")
	
	RecLock("TMP_GRID", .T.)                 

	TMP_GRID->MARK     := "  "
	TMP_GRID->FILIAL   := tmpctes->zbc_codfil
	TMP_GRID->ARQUIVO  := tmpctes->zbc_file
	TMP_GRID->SERIE    := tmpctes->zbc_serie
	TMP_GRID->DOCUMEN  := tmpctes->zbc_doc
	TMP_GRID->EMISSAO  := StoD(tmpctes->zbc_emissa)
	TMP_GRID->FORNECE  := tmpctes->zbc_fornec
	TMP_GRID->LOJA     := tmpctes->zbc_loja
	TMP_GRID->VLTOTAL  := tmpctes->zbc_vltot
	TMP_GRID->VLFRETE  := tmpctes->zbc_vlfre
	TMP_GRID->STATUS   := tmpctes->zbc_proces
	TMP_GRID->PESO     := tmpctes->zbc_peso
	TMP_GRID->VOLUME   := tmpctes->zbc_volume
	TMP_GRID->CALC     := tmpctes->zbc_calc

	TMP_GRID->(MsUnlock())
	
	tmpctes->(DbSkip())
EndDo

tmpctes->(DbCloseArea())

TMP_GRID->(DbGoTop())
                     
ObjectMethod(oMark:oBrowse,"Refresh()")
oDlgMan:Refresh()

RestArea(aArea)

Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³fExecXML ºAutor  ³TOTVS II             º Data ³  27/11/14   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Função responsável por efetuar a importação do arquivo XML º±±
±±º          ³ do Ct-e                                                    º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ MANXMLCTE                                                  º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
*----------------------------------*
Static Function fExecXML(oXML, cFile)
*----------------------------------*

Local cCodCte := PADL(AllTrim(oXML:_cteproc:_cte:_infcte:_ide:_nct:text),09,"0")
Local cChvCte := ""
Local cChvNfe := ""
Local cSerCte := oXML:_cteproc:_cte:_infcte:_ide:_serie:text
Local dDatEmi := StoD(SubStr(oXML:_cteproc:_cte:_infcte:_ide:_dhemi:text, 01, 04) +; 
								      SubStr(oXML:_cteproc:_cte:_infcte:_ide:_dhemi:text, 06, 02) +;
								      SubStr(oXML:_cteproc:_cte:_infcte:_ide:_dhemi:text, 09, 02))
Local cCgcEmi := oXML:_cteproc:_cte:_infcte:_emit:_cnpj:text 
Local cRazEmi := oXML:_cteproc:_cte:_infcte:_emit:_xnome:text

Local cCodFor := Posicione("SA2", 3, xFilial("SA2") + AllTrim(cCgcEmi), "A2_COD")
Local cLojFor := Posicione("SA2", 3, xFilial("SA2") + AllTrim(cCgcEmi), "A2_LOJA")
Local cConPag := Posicione("ZBA", 1, xFilial("ZBA") + cEmpAnt + cFilAnt, "ZBA_CONPAG")
Local cCodPro := Posicione("ZBA", 1, xFilial("ZBA") + cEmpAnt + cFilAnt, "ZBA_CODPRO")
Local cDesPro := Posicione("SB1", 1, xFilial("SB1") + cCodPro, "B1_DESC")
Local cCodNat := Posicione("ZBA", 1, xFilial("ZBA") + cEmpAnt + cFilAnt, "ZBA_NATURE")
Local cCodSeg := Posicione("ZBA", 1, xFilial("ZBA") + cEmpAnt + cFilAnt, "ZBA_CLVL")
Local cCodCC  := Posicione("ZBA", 1, xFilial("ZBA") + cEmpAnt + cFilAnt, "ZBA_CC")
Local cCodTes := ""
Local nValTot := Val(oXML:_cteproc:_cte:_infcte:_vprest:_vtprest:text)
Local nAliIcm := 0

Local aNfOri  := {}
Local nPsoBrt := 0         // Variável para armazenar peso bruto da carga
Local nQtdVol := 0         // Variável para armazenar quantidade de volumes da carga

Local lRet    := .T.        
Local lFound  := .F. 
Local nX      := 0
Local cSerTmp := ""
Local cDocTmp := ""
Local cCST    := ""
Local lSimpNa := .F.			// Variável que indica se o fornecedor é do Simples Nacional

Private lMsErroAuto  := .F.
Private lMsHelpAuto  := .F.
Private cStatus      := '1'

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Busca chave do Ct-e. ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

If Trim(Upper(XMLGetChild(oXML:_cteproc, 2):REALNAME)) == "PROTCTE"
	cChvCte := oXML:_cteproc:_protcte:_infprot:_chcte:text
Else
  cChvCte := SubStr(oXML:_cteproc:_cte:_signature:_signedinfo:_reference:_uri:text, 05, 44)
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Valida o tipo do Ct-e³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

Do Case
	
	Case AllTrim(oXML:_cteproc:_cte:_infcte:_ide:_tpCTe:text) == '1'   // Ct-e de complemento
		cStatus := '4'
		aAdd(aErros, cFile + " => CT-E DE COMPLEMENTO. O DOCUMENTO FOI SINALIZADO COM STATUS 4 PARA VERIFICACAO POSTERIOR.")
	
	Case AllTrim(oXML:_cteproc:_cte:_infcte:_ide:_tpCTe:text) == '2'   // Ct-e de Anulação
		cStatus := '4'
		aAdd(aErros, cFile + " => CT-E DE ANULACAO. O DOCUMENTO FOI SINALIZADO COM STATUS 4 PARA VERIFICACAO POSTERIOR.") 	
	
	Case AllTrim(oXML:_cteproc:_cte:_infcte:_ide:_tpCTe:text) == '3'   // Ct-e de Substituição
		cStatus := '4'
		aAdd(aErros, cFile + " => CT-E DE SUBSTITUICAO. O DOCUMENTO FOI SINALIZADO COM STATUS 4 PARA VERIFICACAO POSTERIOR.") 	
		
EndCase

If cStatus == '1'

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ
	//³Valida existência da chave da NF-e no Ct-e.
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ
	
	Do Case
		Case ValType(XMLGetChild(oXML:_cteproc:_cte:_infcte:_infctenorm:_infdoc, 1)) == "A"
			If Trim(Upper(XMLGetChild(oXML:_cteproc:_cte:_infcte:_infctenorm:_infdoc, 1)[01]:REALNAME)) == "INFNF"
		
				If ValType(oXML:_cteproc:_cte:_infcte:_infctenorm:_infdoc:_infnf[01]) == "O"
					cSerTmp := PADR(AllTrim(oXML:_cteproc:_cte:_infcte:_infctenorm:_infdoc:_infNF[01]:_serie:text),03," ")
					cDocTmp := PADL(AllTrim(oXML:_cteproc:_cte:_infcte:_infctenorm:_infdoc:_infNF[01]:_ndoc:text),09,"0")
				  
	  			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³Cover Your Ass 01                                                               ³
					//³Segundo definição do Sr Alexandre Dekker, a situação onde mais que um documento ³
					//³é vinculado ao mesmo Ct-e não precisa ser tratado porque são casos muito raros. ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					
					MsgInfo("O Ct-e serie "+ AllTrim(cSerCte) +" número "+ AllTrim(cCodCte) +" possui mais que uma NF vinculada a ele, porém, devido a uma definição no desenvolvimento da rotina,"+; 
									" apenas uma delas será considerada para lançamento da pré-nota.","Atenção!")
											
					aNfOri  := fGetNfOri(2, cSerTmp + cDocTmp)
				  
				EndIf
			
			ElseIf Trim(Upper(XMLGetChild(oXML:_cteproc:_cte:_infcte:_infctenorm:_infdoc, 1)[01]:REALNAME)) == "INFNFE"
				If ValType(oXML:_cteproc:_cte:_infcte:_infctenorm:_infdoc:_infnfe[01]) == "O"
					cChvNfe := oXML:_cteproc:_cte:_infcte:_infctenorm:_infdoc:_infnfe[01]:_chave:text
				  
	  			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³Cover Your Ass 02                                                               ³
					//³Segundo definição do Sr Alexandre Dekker, a situação onde mais que um documento ³
					//³é vinculado ao mesmo Ct-e não precisa ser tratado porque são casos muito raros. ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					
					MsgInfo("O Ct-e serie "+ AllTrim(cSerCte) +" número "+ AllTrim(cCodCte) +" possui mais que uma NF vinculada a ele, porém, devido a uma definição no desenvolvimento da rotina,"+; 
									" apenas uma delas será considerada para lançamento da pré-nota.","Atenção!")
											
					aNfOri  := fGetNfOri(1, cChvNfe)
				  
				EndIf
			EndIf
		
		Case ValType(XMLGetChild(oXML:_cteproc:_cte:_infcte:_infctenorm:_infdoc, 1)) == "O"
			If Trim(Upper(XMLGetChild(oXML:_cteproc:_cte:_infcte:_infctenorm:_infdoc, 1):REALNAME)) == "INFNF"
	
				If ValType(oXML:_cteproc:_cte:_infcte:_infctenorm:_infdoc:_infnf) == "O"
					cSerTmp := PADR(AllTrim(oXML:_cteproc:_cte:_infcte:_infctenorm:_infdoc:_infNF:_serie:text),03," ")
					cDocTmp := PADL(AllTrim(oXML:_cteproc:_cte:_infcte:_infctenorm:_infdoc:_infNF:_ndoc:text),09,"0")
				  aNfOri  := fGetNfOri(2, cSerTmp + cDocTmp) 
		  	EndIf
		  
		  ElseIf Trim(Upper(XMLGetChild(oXML:_cteproc:_cte:_infcte:_infctenorm:_infdoc, 1):REALNAME)) == "INFNFE"
		  	
		  	If ValType(oXML:_cteproc:_cte:_infcte:_infctenorm:_infdoc:_infnfe) == "O"
		  		cChvNfe := oXML:_cteproc:_cte:_infcte:_infctenorm:_infdoc:_infnfe:_chave:text
					aNfOri  := fGetNfOri(1, cChvNfe)	
		  		
		  	EndIf
		  	
		  EndIf
			
	EndCase
  
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ6
	//³Busca alíquota de ICMS do XML³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ6
	
	Do Case
		
		Case Trim(Upper(XMLGetChild(oXML:_cteproc:_cte:_infcte:_imp:_icms, 1):REALNAME)) == "ICMS00"
			nAliIcm := Val(oXML:_cteproc:_cte:_infcte:_imp:_icms:_icms00:_picms:text)
			cCST    := oXML:_cteproc:_cte:_infcte:_imp:_icms:_icms00:_cst:text
			
		Case Trim(Upper(XMLGetChild(oXML:_cteproc:_cte:_infcte:_imp:_icms, 1):REALNAME)) == "ICMS20"
			nAliIcm := Val(oXML:_cteproc:_cte:_infcte:_imp:_icms:_icms20:_picms:text)
			cCST    := oXML:_cteproc:_cte:_infcte:_imp:_icms:_icms20:_cst:text
			
		Case Trim(Upper(XMLGetChild(oXML:_cteproc:_cte:_infcte:_imp:_icms, 1):REALNAME)) == "ICMS60"
			nAliIcm := Val(oXML:_cteproc:_cte:_infcte:_imp:_icms:_icms60:_picmsstret:text)
			cCST    := oXML:_cteproc:_cte:_infcte:_imp:_icms:_icms60:_cst:text
			
		Case Trim(Upper(XMLGetChild(oXML:_cteproc:_cte:_infcte:_imp:_icms, 1):REALNAME)) == "ICMS90"
			nAliIcm := Val(oXML:_cteproc:_cte:_infcte:_imp:_icms:_icms90:_picms:text)
      		cCST    := oXML:_cteproc:_cte:_infcte:_imp:_icms:_icms90:_cst:text
			
		Case Trim(Upper(XMLGetChild(oXML:_cteproc:_cte:_infcte:_imp:_icms, 1):REALNAME)) == "ICMSOUTRAUF"
			nAliIcm := Val(oXML:_cteproc:_cte:_infcte:_imp:_icms:_ICMSOutraUF:_pICMSOutraUF:text)
			cCST    := oXML:_cteproc:_cte:_infcte:_imp:_icms:_ICMSOutraUF:_cst:text
		
		Case Trim(Upper(XMLGetChild(oXML:_cteproc:_cte:_infcte:_imp:_icms, 1):REALNAME)) == "ICMSSN"
			nAliIcm := 0    
			cCST    := "SN" // Simples Nacional (Utilizado posteriormente para setar a TES)
			
	EndCase 
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Busca informações de VOLUME do XML³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	
	If ValType(oXML:_cteproc:_cte:_infcte:_infctenorm:_infcarga:_infq) == "O"
	
		If oXML:_cteproc:_cte:_infcte:_infctenorm:_infcarga:_infq:_cUnid:text == "01"         // 01 KG
			nPsoBrt := Val(oXML:_cteproc:_cte:_infcte:_infctenorm:_infcarga:_infq:_qcarga:text)
		ElseIf oXML:_cteproc:_cte:_infcte:_infctenorm:_infcarga:_infq:_cUnid:text == "02"     // 02 TON
			nPsoBrt := Val(oXML:_cteproc:_cte:_infcte:_infctenorm:_infcarga:_infq:_qcarga:text) * 1000
		ElseIf oXML:_cteproc:_cte:_infcte:_infctenorm:_infcarga:_infq:_cUnid:text == "03"     // 03 UNIDADE
			nQtdVol := Val(oXML:_cteproc:_cte:_infcte:_infctenorm:_infcarga:_infq:_qcarga:text)
		EndIf			
	
	ElseIf ValType(oXML:_cteproc:_cte:_infcte:_infctenorm:_infcarga:_infq) == "A"
	
			For nX := 1 to Len(oXML:_cteproc:_cte:_infcte:_infctenorm:_infcarga:_infq)
				If oXML:_cteproc:_cte:_infcte:_infctenorm:_infcarga:_infq[nX]:_cUnid:text == "01"         // 01 KG
					nPsoBrt := Val(oXML:_cteproc:_cte:_infcte:_infctenorm:_infcarga:_infq[nX]:_qcarga:text)
				ElseIf oXML:_cteproc:_cte:_infcte:_infctenorm:_infcarga:_infq[nX]:_cUnid:text == "02"     // 02 TON
					nPsoBrt := Val(oXML:_cteproc:_cte:_infcte:_infctenorm:_infcarga:_infq[nX]:_qcarga:text) * 1000
				ElseIf oXML:_cteproc:_cte:_infcte:_infctenorm:_infcarga:_infq[nX]:_cUnid:text == "03"     // 03 UNIDADE
					nQtdVol := Val(oXML:_cteproc:_cte:_infcte:_infctenorm:_infcarga:_infq[nX]:_qcarga:text)
				EndIf
			Next nX	
			
	EndIf
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Regra para setar TES padrão³                	
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	
	If Posicione("ZBA", 1, xFilial("ZBA") + cEmpAnt + cFilAnt, "ZBA_TOMADO") == 'N'       // Valida se a empresa/filial é tomadora S/N
		DbSelectArea("ZBD")
		ZBD->(DbSetOrder(3))
		If DbSeek(xFilial("ZBD") + cEmpAnt + cFilAnt + cCST)     		                        // Busca a TES configurada para o grupo tributário do fornecedor
			cCodTes := ZBD->ZBD_TES
		ElseIf DbSeek(xFilial("ZBD") + cEmpAnt + cFilAnt + "GE")                            // Busca uma TES genérica para os grupos que não estão com TES configurada
			cCodTes := ZBD->ZBD_TES
		EndIf
	Else
	  cCodTes := Posicione("ZBA", 1, xFilial("ZBA") + cEmpAnt + cFilAnt, "ZBA_TES")       // Se ela for tomadora, pega direto a TES da ZBA
	EndIf  
	
	If Empty(cCodTes)
		MsgInfo("Não encontrei regra válida para setar TES :( Procure corrigir os parâmetros dessa filial!")
	EndIf
 
  //ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ
	//³ Início das validações³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ
	
	if Empty(cCodFor)
		cStatus := '3'
		aAdd(aErros, cFile + " => FORNECEDOR: "+ Trim(cRazEmi) +" CGC: "+ cCgcEmi +" -> NAO ESTA CADASTRADO.")
	EndIf
	
	If Empty(cConPag)
		cStatus := '3'
		aAdd(aErros,  cFile + " =>CONDICAO DE PAGAMENTO INDICADA NO CADASTRO DE PARÂMETROS DA ROTINA É INVÁLIDA. ")
	EndIf
	
	lFound := fFindDoc(cChvCte)
	
	if lFound
		cStatus := '3'
		aAdd(aErros, cFile + " => ENCONTREI UM CTE SERIE "+ AllTrim(cSerCte) +" NRO "+ AllTrim(cCodCte) +;
								 " JA LANCADO PARA O FORNECEDOR "+ Trim(cCodFor) +" LOJA "+ Trim(cLojFor))
	EndIf  
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Fim das validações³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	
	aCabec := {} 
	aItens := {}
	aAdd(aCabec,{"F1_FILIAL" , xFilial("SF1"), Nil, Nil}) 
	aadd(aCabec,{"F1_TIPO"   , "N"           , Nil, Nil})         																				// Complemento de Frete
	aadd(aCabec,{"F1_FORMUL" , "N"           , Nil, Nil})                                                 // Formulário próprio = Não
	aadd(aCabec,{"F1_DOC"    , cCodCte       , Nil, Nil})                                                 // Nro do Cte
	aadd(aCabec,{"F1_CHVNFE" , cChvCte       , Nil, Nil})                                                 // Chave do Ct-e
	aadd(aCabec,{"F1_SERIE"  , cSerCte       , Nil, Nil})                                                 // Série Ct-e
	aadd(aCabec,{"F1_EMISSAO", dDatEmi       , Nil, Nil})                                                 // Emissao
	aadd(aCabec,{"F1_FORNECE", cCodFor       , Nil, Nil})                                                 // Código Fornecedor
	aadd(aCabec,{"F1_LOJA"   , cLojFor       , Nil, Nil})                                                 // Loja Fornecedor
	aadd(aCabec,{"F1_ESPECIE", "CTE"         , Nil, Nil})                                                 // Espécie do Documento
	aAdd(aCabec,{"F1_NATUREZ", cCodNat       , Nil, Nil})                                                 // Natureza de Operação
	aadd(aCabec,{"F1_COND"   , cConPag       , Nil, Nil})                                                 // Condição de pagamento
	aAdd(aCabec,{"F1_VOLUME1", nQtdVol       , Nil, Nil})
	aAdd(aCabec,{"F1_X_PESOB", nPsoBrt       , Nil, Nil})
	                                
	aLinha := {}
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Se for sempre uma nota para cada Ct-e, não será necessário loop para linha de itens³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	
	aAdd(aLinha,{"D1_FILIAL", xFilial("SD1") , Nil, Nil})
	aadd(aLinha,{"D1_COD"   , cCodPro        , Nil, Nil})
	aAdd(aLinha,{"D1_DESCRI", cDesPro        , Nil, Nil}) 
	aadd(aLinha,{"D1_QUANT" , 1              , Nil, Nil}) 
	aadd(aLinha,{"D1_VUNIT" , nValTot        , Nil, Nil}) 
	aadd(aLinha,{"D1_TOTAL" , nValTot        , Nil, Nil})
	aAdd(aLinha,{"D1_X_TES" , cCodTes        , Nil, Nil})
	aAdd(aLinha,{"D1_CC"    , cCodCC         , Nil, Nil})
	
	If nAliIcm > 0
		aAdd(aLinha,{"D1_PICM"  , nAliIcm      , Nil, Nil}) 
	EndIf
	
	If Len(aNfOri) > 0
		
		aadd(aLinha,{"D1_NFORI" , aNfOri[aScan(aNfOri, {|x| AllTrim(x[01]) == "F2_DOC"  })][02], Nil, Nil})
		aAdd(aLinha,{"D1_SERORI", aNfOri[aScan(aNfOri, {|x| AllTrim(x[01]) == "F2_SERIE"})][02], Nil, Nil})	
	
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Campo que armazena o cálculo do valor do frete dividido pelo valor da NF de origem³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	
		aAdd(aCabec,{"F1_X_CALC" , Round( (aLinha[aScan(aLinha, {|x| AllTrim(x[01]) == "D1_TOTAL"})][02] / aNfOri[aScan(aNfOri, {|x| AllTrim(x[01]) == "F2_VALBRUT"})][02]) * 100, TamSX3("F1_X_CALC")[02]) , Nil, Nil})
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Se estiver vazio na nota de origem, busca segmento dos parâmetros da rotina³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		
		if Empty(aNfOri[aScan(aNfOri, {|x| AllTrim(x[01]) == "D2_CLVL"})][02])
			aAdd(aLinha,{"D1_CLVL"  , cCodSeg , Nil, Nil})
		Else
			aAdd(aLinha,{"D1_CLVL"  , aNfOri[aScan(aNfOri, {|x| AllTrim(x[01]) == "D2_CLVL"})][02], Nil, Nil})
		EndIf
	 	  
	  aadd(aItens,aLinha) 
	 	
	 	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Chamada da função para atualizar informações do XML na tabela ZBC³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		
		fGrvInfXml(aCabec, aItens, cFile, aNfOri, nPsoBrt, nQtdVol, cChvCte)
	
	Else
		cStatus := '3'
		aAdd(aErros, cFile + " => NAO CONSEGUI ENCONTRAR A NOTA QUE ORIGINOU O CT-E!" + CHR(13)+CHR(10) +;
								 iif(Empty(cChvNfe), "SERIE DA NF ENCONTRADA NO CT-E..: "+ cSerTmp + CHR(13)+CHR(10) +; 
								 "NRO DA NF ENCONTRADA NO CT-E....: "+ cDocTmp, "CHAVE DA NF-E ENCONTRADA NO CT-E..: "+ cChvNfe) )		
	EndIf

EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ
//³Bloco de código para validar dados antes da execução do ExecAuto³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ

If Len(aErros) > 0
	
	cTmp := "" // Variável temporária para mostar mensagens de erro em tela
	
	For nX := 1 to Len(aErros)
		cTmp += aErros[nX] + CHR(13)+CHR(10)
		aAdd(aResErr, aErros[nX])
	Next nX
	
	fGrvLog(cTmp, cFile, cStatus) 
	
	aErros  := {}
	cStatus := '1'
	
	Return .F.
EndIf

MsgRun("Aguarde gerando Pré-Nota de Entrada...",,{|| MSExecAuto ( {|x,y,z| MATA140(x,y,z) }, aCabec, aItens, 3)})   

If lMsErroAuto 
	lRet := .F.
	cTmp := MostraErro()
	aAdd(aResErr, cFile + " => ERRO NA INCLUSAO DO DOCUMENTO DE ENTRADA. VERIFIQUE OS DETALHES!")
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Gravação do log de processamento, caso tenha ocorrido erro na execução³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	cStatus := '3' 
	
	fGrvLog(cTmp, cFile, cStatus) 

Else
	aAdd(aResAce, cFile + " => OK ")
	
	// 12/03/15 - REMOVIDA POR SOLICITACAO DA EQUIPE OPERACIONAL. SEGUNDO ELES, NAO TEM NECESSIDADE DE MOSTRAR ESSA INFORMACAO.
	//Aviso("Pré-Nota Incluída", "Documento "+ cCodCte +" série "+ cSerCte +" incluído com sucesso!", {"Ok"}, 3)
	
EndIf

fDados()

ObjectMethod(oMark:oBrowse,"Refresh()")
oDlgMan:Refresh()

Return lRet         

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³fFindDoc ºAutor  ³TOTVS II             º Data ³  05/01/15   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Função para validar se o Ct-e já foi incluso.              º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ MANXMLCTE                                                  º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Static Function fFindDoc(cChvCte)

Local lRet := .F.
Local cSql := ""                	

cSql := "select count(*) as quant from "+ RetSqlName("SF1") +" f1 "
cSql += "where f1.d_e_l_e_t_ = ' ' "
cSql += "  and f1.f1_chvnfe = '"+ AllTrim(cChvCte) +"' "

TCQUERY cSql NEW ALIAS "tmpf1" 

DbSelectArea("tmpf1")
tmpf1->(DbGoTop())

lRet := tmpf1->quant > 0

tmpf1->(DbCloseArea())

Return lRet

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³fGrvLog ºAutor  ³TOTVS II              º Data ³  06/01/15   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Função para gravação de log de importação do XML           º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ MANXMLCTE                                                  º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Static Function fGrvLog(cTmp, cFile, cStatus)

Local aArea := GetArea()

DbSelectArea("ZBC")
DbSetOrder(2)

If DbSeek(xFilial("ZBC") + cFile)
	RecLock("ZBC", .F.)
	ZBC->ZBC_PROCESS := cStatus                // Erro de processamento
	ZBC->ZBC_LOG     := OemToAnsi(cTmp)        // Grava log para análise posterior
	ZBC->(MsUnlock())
EndIf

RestArea(aArea)

Return


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³fGrvInfXML ºAutor  ³TOTVS II           º Data ³  06/01/15   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Função para gravar dados do XML na tabela ZBC              º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ MANXMLCTE                                                  º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Static Function fGrvInfXml(aCabec, aItens, cFile, aNfOri, nPeso, nVolume, cChvCte)

Local nPosSer := aScan(aCabec, {|x| AllTrim(x[01]) == "F1_SERIE"})
Local nPosDoc := aScan(aCabec, {|x| AlLTrim(x[01]) == "F1_DOC"})
local nPosEmi := aScan(aCabec, {|x| AllTrim(x[01]) == "F1_EMISSAO"})
local nPosFor := aScan(aCabec, {|x| AllTrim(x[01]) == "F1_FORNECE"})
Local nPosLoj := aScan(aCabec, {|x| AllTrim(x[01]) == "F1_LOJA"})
Local nPosFre := aScan(aItens[01], {|x| AllTrim(x[01]) == "D1_TOTAL"})
Local nPosVlr := aScan(aNfOri, {|x| AllTrim(x[01]) == "F2_VALBRUT"})


DbSelectArea("ZBC")
DbSetOrder(2)
if DbSeek(xFilial("ZBC") + cFile)
	RecLock("ZBC", .F.)                                                
	ZBC->ZBC_SERIE  := aCabec[nPosSer][02]
	ZBC->ZBC_DOC    := aCabec[nPosDoc][02]
	ZBC->ZBC_EMISSA := aCabec[nPosEmi][02]
	ZBC->ZBC_FORNEC := aCabec[nPosFor][02]
	ZBC->ZBC_LOJA   := aCabec[nPosLoj][02]
	ZBC->ZBC_VLTOT  := aNfOri[nPosVlr][02]
	ZBC->ZBC_VLFRE  := aItens[01][nPosFre][02]
	ZBC->ZBC_PESO   := Round(nPeso,   TamSX3("ZBC_PESO"  )[02])
	ZBC->ZBC_VOLUME := Round(nVolume, TamSX3("ZBC_VOLUME")[02])
	ZBC->ZBC_CALC   := iif( nPosVlr > 0, Round((aItens[01][nPosFre][02] / aNfOri[nPosVlr][02]) * 100, TamSX3("ZBC_CALC")[02]), 0 )
	ZBC->ZBC_CHVCTE := cChvCte
	ZBC->(MsUnlock())
EndIf

Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³fGrvTes ºAutor  ³ TOTVS II             º Data ³  12/01/15   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Função para buscar gravar a TES na pré-nota                º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ MANXMLCTE                                                  º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Static Function fGrvTes(aCabec, aItens)

Local nPosSer := aScan(aCabec, {|x| AllTrim(x[01]) == "F1_SERIE"})
Local nPosDoc := aScan(aCabec, {|x| AlLTrim(x[01]) == "F1_DOC"})
local nPosFor := aScan(aCabec, {|x| AllTrim(x[01]) == "F1_FORNECE"})
Local nPosLoj := aScan(aCabec, {|x| AllTrim(x[01]) == "F1_LOJA"})
Local nPosCod := aScan(aItens[01], {|x| AllTrim(x[01]) == "D1_COD"})
Local nPosTes := aScan(aItens[01], {|x| AllTrim(x[01]) == "D1_TES"})
Local cCFOP   := Posicione("SF4", 1, xFilial("SF4") + aItens[01][nPosTes][02], "F4_CF")
Local aArea   := GetArea()

DbSelectArea("SD1")
SD1->(DbSetOrder(2))

If DbSeek(xFilial("SD1") + aItens[01][nPosCod][02] + aCabec[nPosDoc][02] + aCabec[nPosSer][02] + aCabec[nPosFor][02] + aCabec[nPosLoj][02])
	RecLock("SD1", .F.)
	SD1->D1_TES := aItens[01][nPosTes][02]
	SD1->D1_CF  := cCFOP
	SD1->(MsUnlock())
EndIf

RestArea(aArea)

Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³fGetNfOri ºAutor  ³TOTVS II            º Data ³  12/01/15   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Função que retorna dados da NF que originou o Ct-e         º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ MANXMLCTE                                                  º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Static Function fGetNfOri(nOpc, cChvNfe)

Local aArea := GetArea()
Local aRet  := {}
Local cSql  := ""

cSql := "select f2.f2_filial, f2.f2_serie, f2.f2_doc, f2.f2_cliente, f2.f2_loja, f2.f2_valbrut from "+ RetSqlName("SF2") +" f2 "
cSql += "where f2.d_e_l_e_t_ = ' ' "

If nOpc == 1      		// Pesquisa pela chave da NF-e
	cSql += "  and f2.f2_chvnfe = '"+ AllTrim(cChvNfe) +"' "   
	
ElseIf nOpc == 2      // Pesquisa por Filial + Serie + Número NF
	cSql += "  and f2.f2_filial = '"+ xFilial("SF2") +"' " 
	cSql += "  and f2.f2_serie  = '"+ SubStr(cChvNfe, 01, 03) +"' "
	cSql += "  and f2.f2_doc    = '"+ SubStr(cChvNfe, 04, 09) +"' " 
	
EndIf

TcQuery cSql New Alias "notaori"

DbSelectArea("notaori")
notaori->(DbGoTop())

If !notaori->(EOF())
	aAdd(aRet, {"F2_FILIAL",  notaori->f2_filial})
	aAdd(aRet, {"F2_SERIE",   notaori->f2_serie})
	aAdd(aRet, {"F2_DOC",     notaori->f2_doc})
	aAdd(aRet, {"F2_CLIENTE", notaori->f2_cliente})
	aAdd(aRet, {"F2_LOJA",    notaori->f2_loja})
	aAdd(aRet, {"F2_VALBRUT", notaori->f2_valbrut})
Else                    	
	notaori->(DbCloseArea())
	Return aRet
EndIf

notaori->(DbCloseArea())

DbSelectArea("SD2")
SD2->(DbSetOrder(3))
If DbSeek(aRet[01][02] + aRet[03][02] + aRet[02][02] + aRet[04][02] + aRet[05][02])
	aAdd(aRet, {"D2_CLVL",   SD2->D2_CLVL})
	aAdd(aRet, {"D2_CCUSTO", SD2->D2_CCUSTO})
EndIf

RestArea(aArea)

Return aRet

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³fGrvChv ºAutor  ³TOTVS II           º Data ³  21/05/15      º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Essa função tem como objetivo reprocessar toda a tabela    º±±
±±º          ³ de Ct-es para gravar a chave do mesmo no campo ZBC_CHVCTE  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ MANXMLCTE                                                  º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
*--------------------------*
Static Function fGrvChv()
*--------------------------*

Local cSql    := ""
Local aRec    := {}
Local nX      := 0
Local oXML 
Local cAviso  := ""
Local cErro   := ""
Local cChvCte := ""
Local lTransf := .F.

If Aviso("Cuidado!", "Essa rotina tem por objetivo reprocessar toda a tabela de Ct-es (ZBC) gravando no campo ZBC_CHVCTE a chave do documento." +;
										 "Devido à quantidade de registros, essa tarefa pode demorar para ser concluída. Deseja continuar assim mesmo?", {"Sim","Nao"}, 2) != 1
	Return	
EndIf

oLbSta:SetText("Buscando dados... ")
oDlgMan:CommitControls()

cSql := "select zbc.r_e_c_n_o_ as reczbc from "+ RetSqlName("ZBC") +" zbc "
cSql += "where zbc.zbc_chvcte = ' ' "
cSql += "  and zbc.d_e_l_e_t_ = ' ' "

TcQuery cSql New Alias "zbcrep"

DbSelectArea("zbcrep")
zbcrep->(DbGoTop())

If !zbcrep->(EOF())

	While !zbcrep->(EOF())
		aAdd(aRec, zbcrep->reczbc)
		zbcrep->(DbSkip())
	EndDo

Else
	
	MsgInfo("Não há xmls a serem processados! Processo concluído!")
	
	zbcrep->(DbCloseArea())
	oLbSta:SetText(" ")
	oDlgMan:CommitControls()
	
	Return
	
EndIf

zbcrep->(DbCloseArea())

oMtProc:SetTotal(Len(aRec))
oDlgMan:CommitControls()

For nX := 1 to Len(aRec)
	
	oMtProc:Set(nX)
	oLbSta:SetText("Corrigindo registro "+ AllTrim(Str(aRec[nX])) +"...")
	oDlgMan:CommitControls()	

	DbSelectArea("ZBC")
	ZBC->(DbGoTo(aRec[nX]))
	
	oXML := XMLParser( AnsiToOem(ZBC->ZBC_XML) , "_", @cAviso, @cErro)
	
	if ValType(oXML) == "O"		
	
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Valida se o XML não é evento do Ct-e³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		
		cChvCte := ""
		
		if Alltrim(Upper(XMLGetChild(oXML, 1):REALNAME)) != "PROCEVENTOCTE"
				If XmlChildEx(oXML:_CTEPROC, "_PROTCTE") != NIL
					cChvCte := oXML:_cteproc:_protcte:_infprot:_chcte:text		
				EndIf
		EndIf
	
		If !Empty(cChvCte)
			RecLock("ZBC", .F.)
			ZBC->ZBC_CHVCTE := cChvCte
			ZBC->(MsUnlock())
		EndIf
	
	EndIf
	
Next nX

MsgInfo("Processo concluído!")

oMtProc:Set(0)
oLbSta:SetText(" ")
oDlgMan:CommitControls()

Return


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³SCHCLACTE ºAutor  ³TOTVS II           º Data ³   01/06/15   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Schedule executado para classificar pré-notas de Ct-e      º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ ESPECIFICOS CANTU                                          º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

/*
@dinamizar - conteúdo fixo, precisa ser dinalizado
*/

*--------------------------*
User Function SCHCLACTE(lManual, cEmpPar, cFilPar)
*--------------------------*

Local cSql      := ""
Local aRecZbc   := {}
Local nX        := 0
Local lOk       := .F.  

Private aRotina     := MontaMenu()
Private lMsHelpAuto := .T.
PRIVATE lMsErroAuto := .F.

Default lManual := .F.
Default cEmpPar := "31"
Default cFilPar := "01"

fMsgAlert("Iniciando rotina de classificacao de pre-notas", .F.)

If !lManual
	RPCCLEARENV()
	RPCSETTYPE(3)
	PREPARE ENVIRONMENT EMPRESA cEmpPar FILIAL cFilPar MODULO "COM" TABLES "SF1","ZBC" 
EndIf 

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Muda status da rágua de processamento³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

If lManual
	oMtProc:Set(0)
	oLbSta:SetText("Buscando pré-notas...")
	oDlgMan:CommitControls()
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄX¿
//³Busca as pré-notas com status 'pré-nota incluída' para iniciar classificação³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄXÙ

cSql := "select r_e_c_n_o_ as reczbc from "+ RetSqlName("ZBC") +" zbc "
cSql += "where zbc_codemp = '"+ cEmpAnt +"' "
cSql += "  and zbc_codfil = '"+ cFilAnt +"' "
cSql += "  and zbc_proces = '2' "
cSql += "  and zbc_emissa < '"+ DtoS(Date() - 07) +"' "      // @dinamizar
cSql += "  and d_e_l_e_t_ = ' ' "

TCQUERY cSql NEW ALIAS "ZBCTMP"

DbSelectArea("zbctmp")
zbctmp->(DbGoTop())

If zbctmp->(EOF())
	zbctmp->(DbCloseArea())
	fMsgAlert("Nao ha pre-notas a serem classificadas!")
	
	If lManual
  	oMtProc:Set(0)
		oLbSta:SetText(" " )
		oDlgMan:CommitControls()
  EndIf
	
	Return
EndIf

While !zbctmp->(EOF())
	aAdd(aRecZbc, zbctmp->reczbc)
	zbctmp->(DbSkip())
EndDo

zbctmp->(DbCloseArea())

oMtProc:SetTotal(Len(aRecZbc))     // Seta o tamanho da régua

For nX := 1 to Len(aRecZbc)
  
  If lManual
  	oMtProc:Set(nX)
		oLbSta:SetText("Lendo registro ("+ AllTrim(Str(aRecZBC[nX])) +")" )
		oDlgMan:CommitControls()
  EndIf
  
	nRecSF1 := 0
	cChvSF1 := ""
	lOk := .F.
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ9
	//³Posiciona na ZBC pra buscar o número, série e filial da NF³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ9
	
	DbSelectArea("ZBC")
	ZBC->(DbGoTo(aRecZbc[nX]))
	//F1_FILIAL + F1_DOC + F1_SERIE + F1_FORNECE + F1_LOJA + F1_TIPO
	cChvSF1 := ZBC->ZBC_CODFIL + ZBC->ZBC_DOC + ZBC->ZBC_SERIE + ZBC->ZBC_FORNEC + ZBC->ZBC_LOJA
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Posiciona na F1 pra retornar o R_E_C_N_O_ pra passar como parâmetro na rotina de classificação³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		
  DbSelectArea("SF1")
  SF1->(DbSetOrder(1))
  
  If SF1->(DbSeek(cChvSF1))
  
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Posiciona na SD1 pra validar se a NF não foi classificada³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			  		
  		DbSelectArea("SD1")
  		SD1->(DbSetOrder(1))
  		SD1->(DbSeek(cChvSF1))
  		
  		If Empty(SD1->D1_TES)
  			nRecSF1 := SF1->(Recno())
  		Else
  			fMsgAlert("A nota chave "+ AllTrim(cChvSF1) +" ja esta classificada!", .F.)
  			
  			DbSelectArea("ZBC")
  			ZBC->(DbGoTo(aRecZBC[nX]))
  			RecLock("ZBC", .F.) 
  			ZBC->ZBC_PROCES := '7'     // Pré-nota classificada
  			ZBC->(MsUnlock()) 
  			
  			Loop
  		EndIf
  Else
  	fMsgAlert("Nao foi possivel localizar a pre-nota chave "+ AllTrim(cChvSF1))
  	Loop
  EndIf
  
	aPreNta  := fGetPre(cChvSF1)              // Monta dados para MsExecAuto
	
	If nRecSF1 != 0
		if lManual
      
			oLbSta:SetText("Classificando... ("+ cChvSF1 +")" )
			oDlgMan:CommitControls()

			fMsgAlert("Processando pre-nota chave "+ AllTrim(cChvSF1), .F.)
			MsExecAuto({|x,y,z| MATA103(x,y,z)},aPreNta[01],aPreNta[02], 4, .F.) 
			
			If lMsErroAuto
				MostraErro()
			EndIf

		Else
			fMsgAlert("Processando pre-nota chave "+ AllTrim(cChvSF1), .F.)
			MsExecAuto({|x,y,z| MATA103(x,y,z)},aPreNta[01],aPreNta[02], 4, .F.)
		EndIf
	
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³02/06/15 - Valida se o documento foi classificado³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		
		lOk := fAvalCla(cChvSF1)
 			
		If lOk
			DbSelectArea("ZBC")
			ZBC->(DbGoTo(aRecZBC[nX]))
	 			
			RecLock("ZBC", .F.)
			ZBC->ZBC_PROCES := '7'     // Pré-nota classificada
			ZBC->(MsUnlock()) 	
		EndIf
		
	EndIf
	
Next nX

If !lManual
	RESET ENVIRONMENT
EndIf              

If lManual
	
	fDados()
	
	oMtProc:Set(0)
	oLbSta:SetText(" ")
	oDlgMan:CommitControls()
EndIf

fMsgAlert("Fim da rotina de classificacao de pre-notas", .F.)

Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³fMsgAlert ºAutor  ³TOTVS II           º  Data ³  01/06/15   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Função para processar mensagens de acordo com a forma que  º±±
±±º          ³ a rotina está sendo executada.                             º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ MANXMLCTE                                                  º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
*-------------------------------------------*
Static Function fMsgAlert(cMensagem, lManual)
*-------------------------------------------*

Default lManual := .F.

If lManual
	MsgAlert(cMensagem)
Else
	ConOut("SCHCLACTE - "+ UPPER(cMensagem))
EndIf

Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³MontaMenu ºAutor  ³TOTVS II           º  Data ³  02/06/15   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Montagem de inicialização do menu da rotina de classificação±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ MANXMLCTE                                                  º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

*---------------------------*
Static Function MontaMenu()
*---------------------------*

Local aRotina := {}

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Inicializa aRotina para ERP/CRM ou SIGAGSP                   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

aAdd(aRotina,{OemToAnsi("Pesquisar")  , "AxPesqui"   , 0 , 1, 0, .F.}) 		//"Pesquisar"
aAdd(aRotina,{OemToAnsi("Visualizar") , "A103NFiscal", 0 , 2, 0, nil}) 		//"Visualizar"
aAdd(aRotina,{OemToAnsi("Incluir")    , "A103NFiscal", 0 , 3, 0, nil}) 		//"Incluir"
aAdd(aRotina,{OemToAnsi("Classificar"), "A103NFiscal", 0 , 4, 0, nil}) 		//"Classificar"
aAdd(aRotina,{OemToAnsi("Excluir")    , "A103NFiscal", 3 , 5, 0, nil})		//"Excluir"
aAdd(aRotina,{OemToAnsi("Legenda")    , "A103Legenda", 0 , 2, 0, .F.})		//"Legenda"

Return(aRotina)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³fAvalCla  ºAutor  ³TOTVS II           º  Data ³  02/06/15   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Função que faz validação se o documento foi classificado.  º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ MANXMLCTE                                                  º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

*--------------------------------*
Static Function fAvalCla(cChave)
*--------------------------------*

Local lRet  := .F.
Local aArea := GetArea()
	
DbSelectArea("SF1")
SF1->(DbSetOrder(1))
  
If SF1->(DbSeek(cChave))
  
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Posiciona na SD1 pra validar se a NF não foi classificada³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
  		
	DbSelectArea("SD1")
	SD1->(DbSetOrder(1))
	SD1->(DbSeek(cChave))
  		
	If !Empty(SD1->D1_TES)
		lRet := .T.
	EndIf
	
EndIf

RestArea(aArea)

Return lRet

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³fGetPre ºAutor  ³TOTVS II           º Data ³  07/06/15      º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Função que retorna dados da pré-nota para montagem dos     º±±
±±º          ³ vetores para MsExecAuto                                    º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ MANXMLCTE                                                  º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

*-------------------------------------*
Static Function fGetPre(cChave)
*-------------------------------------*

Local aRetorno:= {}
Local aFldCab := {"F1_FILIAL","F1_TIPO","F1_FORMUL","F1_DOC","F1_CHVNFE","F1_SERIE","F1_EMISSAO","F1_FORNECE","F1_LOJA",;
									"F1_ESPECIE","F1_NATUREZ","F1_COND","F1_VOLUME1","F1_X_PESOB", "F1_X_CALC"}
Local aFldIte := {"D1_FILIAL","D1_COD","D1_DESCRI","D1_QUANT","D1_VUNIT","D1_TOTAL","D1_X_TES","D1_TES","D1_CC","D1_PICM",;
									"D1_NFORI","D1_SERIORI","D1_CLVL"}
Local aCab    := {}
Local aIte    := {}
Local aItens  := {}

DbSelectArea("SF1")
SF1->(DbSetOrder(1))
If DbSeek(cChave)
	For nCtrl := 1 to Len(aFldCab)
		aAdd(aCab, {aFldCab[nCtrl], &("SF1->"+aFldCab[nCtrl]), Nil})
	Next nCtrl
	
	aItens := {}
	
	DbSelectArea("SD1")
	SD1->(DbSetOrder(1))
	if SD1->(DBSeek(cChave))
		While !SD1->(EOF()) .and. (SD1->D1_FILIAL + SD1->D1_DOC + SD1->D1_SERIE + SD1->D1_FORNECE + SD1->D1_LOJA == cChave) 
			
			For nCtrl:= 1 to Len(aFldIte)
				If aFldIte[nCtrl] == "D1_TES"
					aAdd(aIte, {aFldIte[nCtrl], SD1->D1_X_TES, Nil})            // Atribui ao campo TES o Tipo de Entrada sugerido na inclusão da pré-nota
				Else
					aAdd(aIte, {aFldIte[nCtrl], &("SD1->"+aFldIte[nCtrl]), Nil})
				EndIf
			Next nCtrl
			
			aAdd(aItens, aIte)
			aIte := {}
			
			SD1->(DbSkip())
		EndDo
	EndIf
	
	aAdd(aRetorno, aCab)
	aAdd(aRetorno, aItens)
		
EndIf

Return aRetorno


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³fClear    ºAutor  ³TOTVS II            º Data ³  10/06/15   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Função para limpar caracteres especiais dos arquivos xmls. º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ MANXMLCTE                                                  º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
*-------------------------------*
Static Function fClear(cBuffer)
*-------------------------------*

Local cRet := ""
Local nX   := 0    
Local cCar := ""

For nX := 1 to Len(cBuffer)
	cCar := SubStr(cBuffer,nX,01)
	If IsAlpha(cCar) .or. isDigit(cCar) .or. cCar$(' ()><#.-:=$?+"/')
		cRet += cCar
	EndIf
Next nX

Return cRet 