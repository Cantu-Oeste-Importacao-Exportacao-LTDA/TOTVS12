#INCLUDE "PROTHEUS.CH"
#INCLUDE "TOPCONN.CH"
#INCLUDE "RWMAKE.CH"
#INCLUDE "TBICONN.CH"
#INCLUDE "TBICODE.CH"

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณXMLXNEO   บAutor  ณJean Carlos Saggin  บ Data ณ  15/12/13   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณ Rotina que farแ a busca dos xmls e a entrega dos mesmos em บฑฑ
ฑฑบ          ณ um diret๓rio que possa ser lido pelo client Mercador       บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ JOB                                                        บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
*------------------------*
User Function XMLXNEO()
*------------------------*

#DEFINE IP_SERVER   "192.168.220.40"             // Endere็o IP do server que estแ rodando o servi็o
#DEFINE PORT_SERVER "20007"                      // Porta configurada para o seri็o no server
#DEFINE EMPINI      "01"                         // Empresa utilizada para dar inํcio no processamento da rotina
#DEFINE FILINI      "01"                         // Filial utilizada para dar inํcio no processamento da rotina
#DEFINE ENTER       CHR(13)+CHR(10)              // Comando enter

Local cSql       := ""
Local lGerado    := .F.
Local cServerIni := GetAdv97()
Local cPort      := GetPvProfString( "TCP", "port", PORT_SERVER, cServerIni) 

ConOut("XMLXNEO - INICIANDO PROCESSAMENTO DOS XMLS")

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณPosiciona o sistema em uma empresa para dar inํcio na sele็ใo dos dadosณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู

RpcClearEnv()
RPCSetType(3)

RpcSetEnv(EMPINI, FILINI,,,,"XMLXNEO",{"SD2", "SF2", "SA1" })

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤ
//ณChama fun็ใo para monitor uso de fontes customizadosณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤ
U_USORWMAKE(ProcName(),FunName())

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณComando SQL para busca das empresas com xmls a enviarณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู

cSql := "select z35.z35_codemp, z35.z35_codfil, count(*) as quant from "+ RetSqlName("Z35") + " z35 " +ENTER
cSql += "where z35.z35_envxml = 'N' and z35.d_e_l_e_t_ <> '*' "                                       +ENTER
cSql += "group by z35.z35_codemp, z35.z35_codfil "                                                    +ENTER
cSql += "order by quant desc "

TcQuery cSql New Alias "empxml"

DbSelectArea("empxml")
empxml->(DbGoTop())

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤ
//ณAvalia se o retorno do SQL for vazioณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤ

if empxml->(eof())
	empxml->(DbCloseArea())
	ConOut("XMLXNEO - SEM ARQUIVOS A ENVIAR")
	Return	
EndIF

aNfs := {}

While !empxml->(eof())
	
	DbSelectArea("Z35")
	Z35->(DbSetOrder(1))
	Z35->(DbSeek(xFilial("Z35") + empxml->z35_codemp + empxml->z35_codfil + "N"))
	
	While (Z35->Z35_CODEMP + Z35->z35_CODFIL + Z35->Z35_ENVXML == empxml->z35_codemp + empxml->z35_codfil + "N")
		aAdd(aNfs, {Z35->Z35_CODEMP, Z35->Z35_CODFIL, Z35->Z35_CODNF, Z35->Z35_SERNF})	
		Z35->(DbSkip())	
	EndDo
	
	StartJob("U_GRVXMLDIR", GetEnvServer(), .T., aNfs, IP_SERVER, PORT_SERVER)
	//U_GRVXMLDIR(aNfs, IP_SERVER, PORT_SERVER)
	
	aNfs := {}
	
	empxml->(DbSkip())
EndDo

empxml->(DbCloseArea())

RPCSetType(3)
RpcSetEnv(EMPINI, FILINI,,,,"XMLXNEO",{"SD2", "SF2", "SA1" })

ConOut("XMLXNEO - FIM DA EXECUCAO")

Return

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณGrvXmlDir   บAutor ณJean Carlos Saggin บ Data ณ  16/12/13   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณ Fun็ใo para grava็ใo do XML no diret๓rio lido pelo client  บฑฑ
ฑฑบ          ณ Mercador                                                   บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ XMLXNEO                                                    บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
*--------------------------------------------------*
User Function GrvXmlDir(aNfs, cIp, cPort)
*--------------------------------------------------*

#DEFINE DIRSRV "\xmlneo\"

Local nX   := 1
Local cSql := ""
Local aXml := {}
Local cXml := ""
Local cXmlEnv    := ""
Local cCabProt   := ""
Local cFinProt   := ""
Local cProtocolo := ""
Local cRetorno   := ""
Local cFileName  := ""
Local lRet       := .F.
Local cEnv       := GetEnvServer()

RpcClearEnv()
RpcSetType(3)
RpcSetEnv(aNfs[01, 01], aNfs[01, 02],,,,GetEnvServer(),)

ConOut("XMLXNEO - EMPRESA: "+ cEmpAnt +" FILIAL: "+ cFilAnt)

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤ
//ณChama fun็ใo para monitor uso de fontes customizadosณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤ
U_USORWMAKE(ProcName(),FunName())

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณAvalia se o diret๓rio existe antes de prosseguirณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู

If !ExistDir(DIRSRV)
	MakeDir(DIRSRV)			
EndIf

for nX := 1 to len(aNfs)

	DbSelectArea("Z35")
	Z35->(DbSetOrder(1))
	if Z35->(DbSeek(xFilial("Z35") + aNfs[nX, 01] + aNfs[nX, 02] + "N" + aNfs[nX, 04] + aNfs[nX, 03]))
		
		ConOut("XMLXNEO - INICIANDO GRAVACAO DO XML SER.: "+ aNfs[nX, 04] +"  NF.: "+ aNfs[nX, 03])
		
		aNota := {} 
		
		//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
		//ณAvalia se a nota ้ de saํdaณ
		//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
		
		if Z35->Z35_TIPONF == "S"
	
			DbSelectArea("SF2")
			SF2->(dbSetOrder(01))
			SF2->(dbSeek(xFilial("SF2") + aNfs[nX, 04] + aNfs[nX, 03]))
	
			aAdd(aNota, .F.)
			aAdd(aNota, Z35->Z35_TIPONF)
			aAdd(aNota, SF2->F2_EMISSAO)
			aAdd(aNota, Z35->Z35_SERNF)
			aAdd(aNota, Z35->Z35_CODNF)
			aAdd(aNota, SF2->F2_CLIENTE)
			aAdd(aNota, SF2->F2_LOJA)
	      
		Else
			
			//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
			//ณCaso for documento de entradaณ
			//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
		
			DbSelectArea("SF1")
			SF1->(dbSetOrder(01))
			SF1->(dbSeek(xFilial("SF1") + Z35->Z35_CODNF + Z35->Z35_SERNF))
			
			aAdd(aNota, .F.)
			aAdd(aNota, Z35->Z35_TIPONF)
			aAdd(aNota, SF1->F1_EMISSAO)
			aAdd(aNota, Z35->Z35_SERNF)
			aAdd(aNota, Z35->Z35_CODNF)
			aAdd(aNota, SF1->F1_FORNECE)
			aAdd(aNota, SF1->F1_LOJA)
			
		EndIf
		
		aNotas := {}
		aAdd(aNotas, aNota)
		cModalidade := "1"
		
		aXml := U_RJGetXML(Z35->Z35_IDENT,aNotas,@cModalidade)
	
		cProtocolo := aXml[01][01]
		cRetorno   := aXml[01][02] 
		
		//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
		//ณMontagem do Nome do arquivo XMLณ
		//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
		
		cFileTmp := SubStr(NfeIdSPED(cRetorno,"Id"),4)
		if !Empty(cFileTmp)    
			cFileName :=  cFileTmp + "-nfe.xml"
		Else
			cFileName := ""
			cFileTmp  := ""
			ConOut("NFEXNEO - FALHA AO MONTAR NOME DO ARQUIVO") 
			
			dbSelectArea("Z35")
			
			RecLock("Z35", .F.)
			Z35->Z35_ENVXML := "E"
			Z35->Z35_DTENV  := dDataBase
			MsUnlock()
			
			Return lRet
		EndIF
		
		//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
		//ณAdiciona o layout do protocolo antes da tag xml da nfeณ
		//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
		
		cCabProt := '<?xml version="1.0" encoding="UTF-8"?>' + chr(13) + chr(10)
		
		//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤLฟ
		//ณse a versao for 3.10 tem que mudar a tag, atualmente deixada fixaณ
		//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤLู
		
		cCabProt += '<nfeProc xmlns="http://www.portalfiscal.inf.br/nfe" versao="3.10">' + chr(13) + chr(10)
		
		cXml := GetXmlCLi(aNfs[nX, 04], aNfs[nX, 03], Z35->Z35_IDENT) + chr(13) + chr(10)
		
		if Empty(cXml)
			ConOut("NFEXNEO - FALHA AO BUSCAR O XML")
			Return lRet
		EndIf
		
		cFinProt := "</nfeProc>"
		
		cRetorno := cRetorno + chr(13) + chr(10)
		
		//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
		//ณmonta o xml com o cabecalho e finalizacao do protocolo de envio, mesmo sem o protocolo da nfe aindaณ
		//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
		
		cXmlEnv := cCabProt + cRetorno + cXml + cFinProt
		
		cFileNeo := DIRSRV + cFileName
		nHdl := FCreate(cFileNeo)
		fWrite(nHdl,cXmlEnv,Len(cXmlEnv))
		FClose(nHdl)      
		
		dbSelectArea("Z35")
			
		RecLock("Z35", .F.)
		Z35->Z35_ENVXML := "S"
		Z35->Z35_DTENV  := dDataBase
		MsUnlock()
		
		lRet := .T.
		
		ConOut("XMLXNEO - XML SER.: "+ aNfs[nX, 04] +"  NF.: "+ aNfs[nX, 03] +" OK.")
			
	EndIf  
	
Next nX

Return lRet

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณRJGETXML   บAutor  ณJean Saggin        บ Data ณ  16/12/13   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณ Rotina que retorna o xml da NF utilizando o TSS            บฑฑ
ฑฑบ          ณ                                                            บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ JOB                                                        บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/

User Function RJGetXML(cIdEnt,aIdNFe,cModalidade)
Local cURL       := PadR(GetNewPar("MV_SPEDURL","http://localhost:8080/sped"),250)
Local oWS
Local cRetorno   := ""
Local cProtocolo := ""
Local cRetDPEC   := ""
Local cProtDPEC  := ""
Local nX         := 0
Local nY         := 0
Local nL		     := 0
Local aRetorno   := {}
Local aResposta  := {}
Local aFalta     := {}
Local aExecute   := {}
Local nLenNFe
Local nLenWS
Local cDHRecbto  := ""
Local cDtHrRec   := ""
Local cDtHrRec1	 := ""
Local nDtHrRec1  := 0
Local lFlag      := .T.
Local dDtRecib	 :=	CToD("")

Private oDHRecbto

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤ
//ณChama fun็ใo para monitor uso de fontes customizadosณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤ
//U_USORWMAKE(ProcName(),FunName())

If Empty(cModalidade)
	oWS := WsSpedCfgNFe():New()
	oWS:cUSERTOKEN := "TOTVS"
	oWS:cID_ENT    := cIdEnt
	oWS:nModalidade:= 0
	oWS:_URL       := AllTrim(cURL)+"/SPEDCFGNFe.apw"
	If oWS:CFGModalidade()
		cModalidade    := SubStr(oWS:cCfgModalidadeResult,1,1)
	Else
		cModalidade    := ""
	EndIf
EndIf
oWS:= WSNFeSBRA():New()
oWS:cUSERTOKEN        := "TOTVS"
oWS:cID_ENT           := cIdEnt
oWS:oWSNFEID          := NFESBRA_NFES2():New()
oWS:oWSNFEID:oWSNotas := NFESBRA_ARRAYOFNFESID2():New()
nLenNFe := Len(aIdNFe)
For nX := 1 To nLenNFe
	aadd(aRetorno,{"","",aIdNfe[nX][4]+aIdNfe[nX][5],"","","",CToD("")})
	aadd(oWS:oWSNFEID:oWSNotas:oWSNFESID2,NFESBRA_NFESID2():New())
	Atail(oWS:oWSNFEID:oWSNotas:oWSNFESID2):cID := aIdNfe[nX][4]+aIdNfe[nX][5]
Next nX
oWS:nDIASPARAEXCLUSAO := 0
oWS:_URL := AllTrim(cURL)+"/NFeSBRA.apw"

If oWS:RETORNANOTASNX()
	If Len(oWs:oWSRETORNANOTASNXRESULT:OWSNOTAS:OWSNFES5) > 0
		For nX := 1 To Len(oWs:oWSRETORNANOTASNXRESULT:OWSNOTAS:OWSNFES5)
			cRetorno        := oWs:oWSRETORNANOTASNXRESULT:OWSNOTAS:OWSNFES5[nX]:oWSNFE:CXML
			cProtocolo      := oWs:oWSRETORNANOTASNXRESULT:OWSNOTAS:OWSNFES5[nX]:oWSNFE:CPROTOCOLO
			cDHRecbto  		:= oWs:oWSRETORNANOTASNXRESULT:OWSNOTAS:OWSNFES5[nX]:oWSNFE:CXMLPROT
			If ValType(oWs:OWSRETORNANOTASNXRESULT:OWSNOTAS:OWSNFES5[nX]:OWSDPEC)=="O"
				cRetDPEC        := oWs:oWSRETORNANOTASNXRESULT:OWSNOTAS:OWSNFES5[nX]:oWSDPEC:CXML
				cProtDPEC       := oWs:oWSRETORNANOTASNXRESULT:OWSNOTAS:OWSNFES5[nX]:oWSDPEC:CPROTOCOLO
			EndIf
			//Tratamento para gravar a hora da transmissao da NFe
			If !Empty(cProtocolo)
				oDHRecbto		:= XmlParser(cDHRecbto,"","","")
				cDtHrRec		:= IIf(Type("oDHRecbto:_ProtNFE:_INFPROT:_DHRECBTO:TEXT")<>"U",oDHRecbto:_ProtNFE:_INFPROT:_DHRECBTO:TEXT,"")
				nDtHrRec1		:= RAT("T",cDtHrRec)
				
				If nDtHrRec1 <> 0
					cDtHrRec1   :=	SubStr(cDtHrRec,nDtHrRec1+1)
					dDtRecib	:=	SToD(StrTran(SubStr(cDtHrRec,1,AT("T",cDtHrRec)-1),"-",""))
				EndIf

			EndIf
			nY := aScan(aIdNfe,{|x| x[4]+x[5] == SubStr(oWs:oWSRETORNANOTASNXRESULT:OWSNOTAS:OWSNFES5[nX]:CID,1,Len(x[4]+x[5]))})
			If nY > 0
				aRetorno[nY][1] := cProtocolo
				aRetorno[nY][2] := cRetorno
				aRetorno[nY][4] := cRetDPEC
				aRetorno[nY][5] := cProtDPEC
				aRetorno[nY][6] := cDtHrRec1
				aRetorno[nY][7] := dDtRecib
				
				aadd(aResposta,aIdNfe[nY])
			EndIf
			cRetDPEC := ""
			cProtDPEC:= ""
		Next nX
		For nX := 1 To Len(aIdNfe)
			If aScan(aResposta,{|x| x[4] == aIdNfe[nX,04] .And. x[5] == aIdNfe[nX,05] })==0
				aadd(aFalta,aIdNfe[nX])
			EndIf
		Next nX
		If Len(aFalta)>0
			aExecute := U_RJGetXML(cIdEnt,aFalta,@cModalidade)
		Else
			aExecute := {}
		EndIf                       
		For nX := 1 To Len(aExecute)
			nY := aScan(aRetorno,{|x| x[3] == aExecute[nX][03]})
			If nY == 0
				aadd(aRetorno,{aExecute[nX][01],aExecute[nX][02],aExecute[nX][03]})
			Else
				aRetorno[nY][01] := aExecute[nX][01]
				aRetorno[nY][02] := aExecute[nX][02]
			EndIf
		Next nX
	EndIf
Else
	ConOut(IIf(Empty(GetWscError(3)),GetWscError(1),GetWscError(3)))
EndIf

Return(aRetorno)

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณGetXmlCli   บAutor  ณJean Saggin       บ Data ณ  16/12/13   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณ Fun็ใo que faz a busca do XML atrav้s do TSS               บฑฑ
ฑฑบ          ณ                                                            บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ AP                                                        บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
*--------------------------------------*
Static Function GetXmlCLi(cSerie, cNF, cIdent)
*--------------------------------------*
Local nConSped := 0
Local cXmlCli := ""
Local cConSped := SuperGetMV("MV_X_SCON", , '{"ORACLE/NFE","192.168.220.5",7891}') //Producao
Local aConSped := &cConSped
Local cSql := ""
Local cNfSerie := PadL(cSerie, TAMSX3("F2_SERIE")[1]) + cNF
Local aArea := GetArea()
Local cTabela := "SPED054"
Local cAlias := "TMP"

TCConType("TCPIP")

nConSped  := TCLink(aConSped[1],aConSped[2],aConSped[3])

If nConSped < 0
	ConOut("Falha na conexใo TOPCONN para buscar o xml. Erro " + Alltrim(Str(nConSped)))
	TCUnLink(nConSped)
	break
	Return ""
Endif

TCSetConn(nConSped)

BeginSql Alias "TMP054"
  SELECT SPED054.R_E_C_N_O_ RNO FROM SPED054 LEFT JOIN SPED001 ON SPED054.ID_ENT = SPED001.ID_ENT 
  WHERE sped001.id_ent = %EXP:cIdent% AND NFE_ID = %EXP:cNfSerie% AND CSTAT_SEFR = '100' 
  ORDER BY LOTE DESC
EndSql

dbSelectArea("TMP054")

nRec := TMP054->RNO

TMP054->(dbCloseArea())

Use &(cTabela) ALIAS &(cAlias) SHARED NEW VIA "TOPCONN"

if Select(cAlias) > 0

	dbSelectArea("TMP")

	TMP->(dbGoTo(nRec))

	cXmlCli := TMP->XML_PROT

	TMP->(dbCloseArea())
EndIf

TCUnLink(nConSped)

RestArea(aArea)

Return cXmlCli